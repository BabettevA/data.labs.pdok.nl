prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix cbs: <http://betalinkeddata.cbs.nl/def/cbs#>
prefix def: <http://betalinkeddata.cbs.nl/def/83487NED#>
prefix dimension: <http://betalinkeddata.cbs.nl/def/dimension#>
prefix energielabels: <https://data.labs.pdok.nl/def/energielabels/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://data.labs.pdok.nl/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?postcode ?huisnummer ?openbareRuimteNaam ?woonplaatsNaam ?oppervlakte ?pandStatusLabel ?supermarkt ?huisarts ?kinderdagverblijf ?school {
  bind("Apeldoorn" as ?woonplaatsNaam)
  bind("Hofstraat" as ?openbareRuimteNaam)
  bind(110 as ?huisnummer)
  graph ?woonplaatsVoorkomen {
    ?woonplaats bag:naamWoonplaats ?woonplaatsNaam.
  }
  filter not exists { ?woonplaatsVoorkomen bag:eindGeldigheid [] }
  graph ?openbareRuimteVoorkomen {
    ?openbareRuimte bag:bijbehorendeWoonplaats ?woonplaats;
                    bag:naamOpenbareRuimte ?openbareRuimteNaam.
  }
  filter not exists { ?openbareRuimteVoorkomen bag:eindGeldigheid [] }
  graph ?nummeraanduidingVoorkomen {
    ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte;
                      bag:huisnummer ?huisnummer;
                      bag:postcode ?postcode.
  }
  filter not exists { ?nummeraanduidingVoorkomen bag:eindGeldigheid [] }
  graph ?verblijfsobjectVoorkomen {
    ?verblijfsobject bag:hoofdadres ?nummeraanduiding;
                     bag:oppervlakte ?oppervlakte;
                     bag:pandrelatering ?pand;
                     bag:status ?verblijfsobjectStatus.
  }
  filter not exists { ?verblijfsobjectVoorkomen bag:eindGeldigheid [] }
  ?verblijfsobjectStatus rdfs:label ?verblijfsobjectStatusLabel.
  graph ?pandVoorkomen {
    ?pand bag:geometriePand/geo:asWKT ?shape;
          bag:oorspronkelijkBouwjaar ?bouwjaar;
          bag:status ?pandStatus.
  }
  filter not exists { ?pandVoorkomen bag:eindGeldigheid [] }
  ?pandStatus rdfs:label ?pandStatusLabel
  service <https://betalinkeddata.cbs.nl/sparql> {
    ?shape geo:sfWithin ?geometry.
      _:1 def:nabijheidVoorzieningen_AfstandTotGroteSupermarkt ?supermarkt ;
          dimension:regio ?buurt .
      _:2 def:nabijheidVoorzieningen_AfstandTotHuisartsenpraktijk ?huisarts ;
          dimension:regio ?buurt .
      _:3 def:nabijheidVoorzieningen_AfstandTotKinderdagverblijf ?kinderdagverblijf ;
          dimension:regio ?buurt .
      _:4 def:nabijheidVoorzieningen_Basisonderwijs_AfstandTotSchool ?school ;
          dimension:regio ?buurt .
    ?buurt geo:hasGeometry ?geometry;
           a cbs:Buurt.
  }
}
