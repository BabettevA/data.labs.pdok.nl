prefix dce: <http://purl.org/dc/elements/1.1/>
prefix schema: <http://schema.org/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?widget ?n {
  {
    select ?subject (sample(?url) as ?url) (count(?image) as ?n) {
      ?image a schema:Photograph;
        dce:subject ?subject;
        schema:thumbnail ?url.
    }
    group by ?subject
    order by desc(?n)
  }
  bind(concat(
    '<div style="position: relative;">',
    '<img src="',str(?url),'" style="max-width: 325px;">',
    '<h2 style="position: absolute; bottom: -11px; left: 0; width: 100%;">',
    '<span style="color: white; font: bold 24px/45px Helvetica, Sans-Serif; letter-spacing: -1px; background: rgb(0, 0, 0); background: rgba(0, 0, 0, 0.7); padding: 10px;">',
    str(?subject),'<br><span style="padding:0 5px;"></span>',
    replace(str(?n),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),
    ' afbeeldingen</span></h2></div>') as ?widget)
}
limit 25
