prefix dc: <http://purl.org/dc/elements/1.1/>
prefix schema: <http://schema.org/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select * {
  {
    select
      ?image
      (strdt(?_date, xsd:date) as ?date)
      ?description
      (group_concat(distinct ?format; separator=', ') as ?format)
      (group_concat(distinct ?subject; separator=', ') as ?subject)
      (min(str(?url)) as ?url)
    {
      ?image dc:date ?_date ;
             dc:description ?description ;
             dc:format ?format ;
             dc:subject ?subject ;
             schema:contentLocation "Apeldoorn"^^xsd:string ;
             schema:thumbnail ?url .
      filter(str(?_date) != "000-00-00")
    }
    group by ?image ?_date ?description
  }
  bind(concat('<img src="',?url,'">',
      '<p><b>Datum</b>: ',str(year(?date)),'-',str(month(?date)),'-',str(day(?date)),
      '<br><b>Beschrijving</b>: ',?description,
      if(?format='','',concat('<br><b>Formaten</b>: ',?format,'.')),
      if(?subject='','',concat('<br><b>Onderwerpen</b>: ',?subject,'.')),
      '') as ?widget)
}
order by desc(?date)
limit 100
