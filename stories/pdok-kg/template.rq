prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix labs: <https://data.labs.pdok.nl/bag/def/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select * {
  #?shape ?shapeLabel ?shapeName {
  {
    select ?bouwjaar ?functie ?huisnummerMin ?huisnummerMax ?oppervlakte ?pand ?pandStatusLabel ?postcodeMin ?postcodeMax ?shape (max(?shapeHeight0) as ?shapeHeight) ?straatNaam ?woonplaatsNaam {
      {
        select ?bouwjaar (group_concat(distinct substr(str(?functie0),65);separator=' ') as ?functie) (min(?huisnummer) as ?huisnummerMin) (max(?huisnummer) as ?huisnummerMax) (sum(?oppervlakte0) as ?oppervlakte) ?pand ?pandStatusLabel (min(?postcode) as ?postcodeMin) (max(?postcode) as ?postcodeMax) ?shape ?straatNaam ?woonplaatsNaam {
          bind("Amsterdam"@nl as ?woonplaatsNaam)
          bind("De Boelelaan"^^xsd:string as ?straatNaam)
          #bind("1094EH"^^xsd:string as ?postcode)
          #bind(1111 as ?huisnummer)
          #bind("B" as ?huisnummertoevoeging)
          #bind("a" as ?huisletter)
          graph ?g0 {
            ?place rdfs:label ?woonplaatsNaam.
          }
          filter not exists {
            ?g0 bag:eindGeldigheid ?eind0.
          }
          graph ?g1 {
            ?openbareRuimte
              bag:bijbehorendeWoonplaats ?place;
              bag:naamOpenbareRuimte ?straatNaam.
          }
          filter not exists {
            ?g1 bag:eindGeldigheid ?eind1.
          }
          graph ?g2 {
            ?nummeraanduiding
              bag:bijbehorendeOpenbareRuimte ?openbareRuimte;
              bag:huisnummer ?huisnummer;
              bag:postcode ?postcode.
            optional {
              ?nummeraanduiding bag:huisletter ?huisletter.
            }
            optional {
              ?nummeraanduiding bag:huisnummertoevoeging ?huisnummertoevoeging.
            }
          }
          filter not exists {
            ?g2 bag:eindGeldigheid ?eind2.
          }
          graph ?g3 {
            ?verblijfsobject a ?functie0;
              bag:hoofdadres ?nummeraanduiding;
              bag:oppervlakte ?oppervlakte0;
              bag:pandrelatering ?pand.
          }
          filter not exists {
            ?g3 bag:eindGeldigheid ?eind3.
          }
          filter(?functie0 not in (bag:Verblijfsobject, geo:Feature))
          graph ?g4 {
            ?pand
              bag:geometriePand/geo:asWKT ?shape;
              bag:oorspronkelijkBouwjaar ?bouwjaar;
              bag:status ?pandStatus.
          }
          filter not exists {
            ?g4 bag:eindGeldigheid ?eind4.
          }
          ?pandStatus rdfs:label ?pandStatusLabel.
        }
        group by ?bouwjaar ?pand ?pandStatusLabel ?shape ?straatNaam ?woonplaatsNaam
        limit 1000
      }
      service <https://data.labs.pdok.nl/migratie/sparql> {
        ?pand labs:measuredHeight ?shapeHeight0.
      }
    }
    group by ?bouwjaar ?functie ?huisnummerMin ?huisnummerMax ?oppervlakte ?pand ?pandStatusLabel ?postcodeMin ?postcodeMax ?shape ?straatNaam ?woonplaatsNaam
  }
  bind(strbefore(str(?shapeHeight),'.') as ?beginning)
  bind(strafter(str(?shapeHeight),'.') as ?end1)
  bind(if(strlen(?end1)>2,substr(?end1,1,2),?end1) as ?end2)
  bind(concat(?beginning,'.',?end2) as ?hoogte)
  bind(if(?huisnummerMin=?huisnummerMax,str(?huisnummerMin),concat(str(?huisnummerMin),'-',str(?huisnummerMax),if(bound(?huisnummertoevoeging),concat('-',str(?huisnummertoevoeging)),''),if(bound(?huisletter),concat(' ',str(?huisletter)),''))) as ?huisnummerLabel)
  bind(if(?postcodeMin=?postcodeMax,str(?postcodeMin),concat(str(?postcodeMin),'-',str(?postcodeMax))) as ?postcodeLabel)
  bind(concat(str(?straatNaam),' ',?huisnummerLabel) as ?shapeName)
  bind(concat(
      '<h2>',str(?shapeName),'<br>',str(?postcodeLabel),' ',str(?woonplaatsNaam),'</h2><ul>',
      '<li><b>Oorspronkelijk bouwjaar</b>: ',str(?bouwjaar),'</li>',
      '<li><b>Pand hoogte</b>: ',if(bound(?hoogte),concat(?hoogte,'m'),'onbekend'),'</li>',
      '<li><b>Pand status</b>: ',str(?pandStatusLabel),'</li>',
      '<li><b>Verblijfsobject oppervlakte</b>: ',replace(str(?oppervlakte),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),'m2</li>',
      '<li><b>Verblijfsobject functies</b>: ',str(?functie),'</li>',
      '</ul>') as ?shapeLabel)
}
limit 100
