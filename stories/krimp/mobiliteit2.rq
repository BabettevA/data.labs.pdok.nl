# Afstand tot voorzieningen in krimpregio's.
prefix cbs: <https://data.pdok.nl/cbs/vocab/>
prefix dct: <http://purl.org/dc/terms/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?wkt ?wktLabel {
  values (?_oprit ?_trein ?_kind ?_boodschap ?_auto ?_nijverheid ?_landbouw ?_totaal) {
         (1.39    8.03    1.48   0.97        1.16   5.185        0.1383     32.2277 ) }
  ?gemeente a cbs:Gemeente ;
            dct:partOf [] ;
            geo:hasGeometry/geo:asWKT ?wkt ;
            rdfs:label ?label ;
            cbs:verkeerenvervoer_Afstandtotoprithoofdverkeersweg_km ?oprit ;
            cbs:verkeerenvervoer_Treinstations_Afstandtottreinstationstotaal_km ?trein ;
            cbs:kinderopvang_Kinderdagverblijf_Afstandtotkinderdagverblijf_km ?kind ;
            cbs:detailhandel_Winkelsdagelijkseboodschappen_Afstandtotov.dagel.levensmiddelen_km ?boodschap ;
            cbs:motorvoertuigen_Personenauto_s_Personenauto_sperhuishouden_perhuishouden ?auto ;
            cbs:werkgelegenheid_aantalbanen_B_Fnijverheidenenergie_Binnen10km_x1000 ?nijverheid ;
            cbs:werkgelegenheid_aantalbanen_Alandbouw_bosbouwenvisserij_Binnen10km_x1000 ?landbouw ;
            cbs:werkgelegenheid_aantalbanen_A_Ualleeconomischeactiviteiten_Binnen10km_x1000 ?totaal .
  bind(concat(
    '<h4>Afstanden tot voorzieningen voor krimpgemeente â€˜',str(?label),'â€™</h4>',
    if(?oprit<?_oprit,'ðŸ ‹','ðŸ ‰'),' Oprit hoofdverkeersweg: ',str(?oprit), ' km (gem. ',str(?_oprit),' km)<br>',
    if(?trein<?_trein,'ðŸ ‹','ðŸ ‰'),' Treinstation: ',str(?trein),' km (gem. ',str(?_trein),' km)<br>',
    if(?kind<?_kind,'ðŸ ‹','ðŸ ‰'),' Kinderopvang: ',str(?kind),' km (gem. ',str(?_kind),' km)<br>',
    if(?boodschap<?_boodschap,'ðŸ ‹','ðŸ ‰'),' Dagelijkse boodschappen: ',str(?boodschap),' km (gem. ',str(?_boodschap),' km)',
    '<h4>Autobezit</h4>',
    if(?auto<?_auto,'ðŸ ‹','ðŸ ‰'),' Aantal auto\'s per huishouden: ',str(?auto),' ðŸš— (gem. ',str(?_auto),' ðŸš—)</p>',
    '<h4>Aantal banen binnen 10 km</h4>',
    if(?nijverheid<?_nijverheid,'ðŸ ‹','ðŸ ‰'),' Nijverheid &amp; energie: ',str(xsd:int(?nijverheid*1000)),' ðŸ‘· (gem. ',str(xsd:int(?_nijverheid*1000)),')<br>',
    if(?landbouw<?_landbouw,'ðŸ ‹','ðŸ ‰'),' Landbouw, bowbouw &amp; visserij: ',str(xsd:int(?landbouw*1000)),' x1.000 ðŸ‘· (gem. ',str(xsd:int(?_landbouw*1000)),' x1.000)<br>',
    if(?totaal<?_totaal,'ðŸ ‹','ðŸ ‰'),' Totaal: ',str(xsd:int(?totaal*1000)),' x1.000 ðŸ‘· (gem. ',str(xsd:int(?_totaal*1000)),' x1.000)</p>'
  ) as ?wktLabel)
}
limit 100
