# Afstand tot voorzieningen in krimpregio's.
prefix dct: <http://purl.org/dc/terms/>
prefix cbs: <https://data.pdok.nl/cbs/vocab/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?wkt ?wktLabel {
  bind (1.39 as ?_oprit)
  bind (8.03 as ?_trein)
  bind (1.48 as ?_kind)
  bind (0.97 as ?_boodschap)
  bind (1.16 as ?_auto)
  bind (62.88 as ?_arbeid)
  ?gemeente a cbs:Gemeente ;
    geo:hasGeometry/geo:asWKT ?wkt ;
    rdfs:label ?label ;
    dct:partOf [] ;
    cbs:verkeerenvervoer_Afstandtotoprithoofdverkeersweg_km ?oprit ;
    cbs:verkeerenvervoer_Treinstations_Afstandtottreinstationstotaal_km ?trein ;
    cbs:kinderopvang_Kinderdagverblijf_Afstandtotkinderdagverblijf_km ?kind ;
    cbs:detailhandel_Winkelsdagelijkseboodschappen_Afstandtotov.dagel.levensmiddelen_km ?boodschap ;
    cbs:nettoarbeidsparticipatie_p ?arbeid ;
    cbs:motorvoertuigen_Personenauto_s_Personenauto_sperhuishouden_perhuishouden ?auto .
  bind (concat(
    '<h4>Afstanden tot voorzieningen voor krimpgemeente â€˜',?label,'â€™</h4>',
    '<p>',if(?oprit<?_oprit,'ðŸ ‹','ðŸ ‰'),' Oprit hoofdverkeersweg: ',?oprit, 'km (gem. ',?_oprit,' km)<br>',
    if(?trein<?_trein,'ðŸ ‹','ðŸ ‰'),' Treinstation: ',?trein,' km (gem. ',?_trein,' km)<br>',
    if(?kind<?_kind,'ðŸ ‹','ðŸ ‰'),' Kinderopvang: ',?kind,' km (gem. ',?_kind,' km)<br>',
    if(?boodschap<?_boodschap,'ðŸ ‹','ðŸ ‰'),' Dagelijkse boodschappen: ',?boodschap,' km (gem. ',?_boodschap,' km)</p>',
    '<h4>Autobezit &amp; Arbeidsparticipatie</h4>',
    if(?auto<?_auto,'ðŸ ‹','ðŸ ‰'),' Aantal auto\'s per huishouden: ',?auto,' ðŸš— (gem. ',?_auto,' ðŸš—)</p>',
    if(?arbeid<?_arbeid,'ðŸ ‹','ðŸ ‰'),' Arbeidsparticipatie: ',?arbeid,'% (gem. ',?_arbeid,'%)</p>'
  ) as ?wktLabel)
}
limit 100
