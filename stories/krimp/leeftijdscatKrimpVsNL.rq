PREFIX cbs: <https://data.pdok.nl/cbs/vocab/>
PREFIX wibu: <https://data.pdok.nl/cbs/vocab/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX krimp: <https://data.pdok.nl/cbs/id/krimpgebied/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?regionaam ?factor_inwnrs_0_15_jr ?factor_inwnrs_15_25_jr ?factor_inwnrs_25_45_jr ?factor_inwnrs_45_65_jr ?factor_inwnrs_65_jr_plus
where {
  {
    select ?regionaam
    ((?inw_0_15 / ?inw_tot) as ?factor_inwnrs_0_15_jr )
    ((?inw_15_25 / ?inw_tot) as ?factor_inwnrs_15_25_jr )
    ((?inw_25_45 / ?inw_tot) as ?factor_inwnrs_25_45_jr )
    ((?inw_45_65 / ?inw_tot) as ?factor_inwnrs_45_65_jr )
    ((?inw_65_ouder / ?inw_tot) as ?factor_inwnrs_65_jr_plus )
    {
      select ?regionaam (sum(?g_tot) as ?inw_tot )
      (sum(?g_lg1) as ?inw_0_15 )
      (sum(?g_lg2) as ?inw_15_25 )
      (sum(?g_lg3) as ?inw_25_45 )
      (sum(?g_lg4) as ?inw_45_65 )
      (sum(?g_lg5) as ?inw_65_ouder )
      where {
        {
          select *
          where {
            graph <https://data.pdok.nl/cbs/krimpgebieden_2016> {
              ?gemeente a cbs:Gemeente .
              ?regio ^dct:partOf ?gemeente .
    	      filter(?regio = krimp:DeMarne )
            }
            graph <https://data.pdok.nl/cbs/wijken_buurten_2016> {
              ?gemeente wibu:bevolking_Aantalinwoners_aantal ?g_tot ;
                        wibu:bevolking_Leeftijdsgroepen_0tot15jaar_aantal ?g_lg1 ;
                        wibu:bevolking_Leeftijdsgroepen_15tot25jaar_aantal ?g_lg2 ;
                        wibu:bevolking_Leeftijdsgroepen_25tot45jaar_aantal ?g_lg3 ;
                        wibu:bevolking_Leeftijdsgroepen_45tot65jaar_aantal ?g_lg4 ;
                        wibu:bevolking_Leeftijdsgroepen_65jaarofouder_aantal ?g_lg5 ;
                        .
            }
          }
        }
        ?regio rdfs:label ?regionaam
      }
      group by ?regionaam
    }
  }
  union {
    select ?regionaam
    ((?inw_0_15 / ?inw_tot) as ?factor_inwnrs_0_15_jr )
    ((?inw_15_25 / ?inw_tot) as ?factor_inwnrs_15_25_jr )
    ((?inw_25_45 / ?inw_tot) as ?factor_inwnrs_25_45_jr )
    ((?inw_45_65 / ?inw_tot) as ?factor_inwnrs_45_65_jr )
    ((?inw_65_ouder / ?inw_tot) as ?factor_inwnrs_65_jr_plus )
    {
      select ?regionaam (sum(?g_tot) as ?inw_tot )
      (sum(?g_lg1) as ?inw_0_15 )
      (sum(?g_lg2) as ?inw_15_25 )
      (sum(?g_lg3) as ?inw_25_45 )
      (sum(?g_lg4) as ?inw_45_65 )
      (sum(?g_lg5) as ?inw_65_ouder )
      where {
        {
          select *
          where {
            graph <https://data.pdok.nl/cbs/wijken_buurten_2016> {
              ?nl a cbs:Land ;
                  wibu:bevolking_Aantalinwoners_aantal ?g_tot ;
                  wibu:bevolking_Leeftijdsgroepen_0tot15jaar_aantal ?g_lg1 ;
                  wibu:bevolking_Leeftijdsgroepen_15tot25jaar_aantal ?g_lg2 ;
                  wibu:bevolking_Leeftijdsgroepen_25tot45jaar_aantal ?g_lg3 ;
                  wibu:bevolking_Leeftijdsgroepen_45tot65jaar_aantal ?g_lg4 ;
                  wibu:bevolking_Leeftijdsgroepen_65jaarofouder_aantal ?g_lg5 ;
                  wibu:regioaanduiding_Gemeentenaam_naam ?regionaam
              .
            }
          }
        }
      }
      group by ?regionaam
    }
  }
}
