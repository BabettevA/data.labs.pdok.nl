PREFIX cbs: <https://data.pdok.nl/cbs/vocab/>
PREFIX wibu: <https://data.pdok.nl/cbs/vocab/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX krimp: <https://data.pdok.nl/cbs/id/krimpgebied/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?wijk ?wkt ?wktLabel ?wktColor
where {
  graph <https://data.pdok.nl/cbs/krimpgebieden_2016> {
    ?regio ^dct:partOf ?gemeente .
    filter(?regio = krimp:DeMarne )
  }
  graph <https://data.pdok.nl/cbs/graph/2016> {
    ?wijk geo:sfWithin ?gemeente .
  }
  graph <https://data.pdok.nl/cbs/wijken_buurten_2016> {
    ?gemeente a cbs:Gemeente .
    ?wijk a cbs:Wijk ;
          wibu:bevolking_Aantalinwoners_aantal ?w_tot ;
          wibu:bevolking_Leeftijdsgroepen_0tot15jaar_aantal ?w_lg1 ;
          wibu:bevolking_Leeftijdsgroepen_15tot25jaar_aantal ?w_lg2 ;
          wibu:bevolking_Leeftijdsgroepen_25tot45jaar_aantal ?w_lg3 ;
          wibu:bevolking_Leeftijdsgroepen_45tot65jaar_aantal ?w_lg4 ;
          wibu:bevolking_Leeftijdsgroepen_65jaarofouder_aantal ?w_lg5 ;
          geo:hasGeometry/geo:asWKT ?wkt ;
          wibu:regioaanduiding_Codering_code ?wijkcode ;
          wibu:regioaanduiding_Gemeentenaam_naam ?wijkgem ;
   	.
    bind(concat(?wijkcode, " (", ?wijkgem, ")") as ?wijkLabel)
    bind(substr(str(?w_lg1 / ?w_tot * 100), 1, 4) as ?0_15_jr)
    bind(substr(str(?w_lg2 / ?w_tot * 100), 1, 4) as ?15_25_jr)
    bind(substr(str(?w_lg3 / ?w_tot * 100), 1, 4) as ?25_45_jr)
    bind(substr(str(?w_lg4 / ?w_tot * 100), 1, 4) as ?45_65_jr)
    bind(substr(str(?w_lg5 / ?w_tot * 100), 1, 4) as ?65_jr_plus)
    bind(concat("<h3>Leeftijdsopbouw</h3>",
        		"<h4>", ?wijkLabel, "</h4>",
        		"<p>leeftijd 0 tot 15 jaar: <b>", ?0_15_jr, "%</b></p>" ,
        		"<p>leeftijd 15 tot 25 jaar: <b>", ?15_25_jr, "%</b></p>" ,
        		"<p>leeftijd 25 tot 45 jaar: <b>", ?25_45_jr, "%</b></p>" ,
        		"<p>leeftijd 45 tot 65 jaar: <b>", ?45_65_jr, "%</b></p>" ,
        		"<p>leeftijd 65 jaar en ouder: <b>", ?65_jr_plus, "%</b></p>") as ?wktLabel)
  }
  values (?regio ?wktColor) {
    ( krimp:Achterhoek  "orangered")
    ( krimp:DeMarne  "violet" )
    ( krimp:Eemsdelta  "teal" )
    ( krimp:Maastricht_Mergelland  "rebeccapurple" )
    ( krimp:Noord_OostFriesland  "midnightblue" )
    ( krimp:Oost_Groningen  "slateblue" )
    ( krimp:ParkstadLimburg  "mediumvioletred" )
    ( krimp:WestelijkeMijnstreek  "olive" )
    ( krimp:Zeeuws_Vlaanderen  "indianred" )
  }
}
