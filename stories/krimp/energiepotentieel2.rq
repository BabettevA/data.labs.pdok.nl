prefix cbs: <https://data.pdok.nl/cbs/vocab/>
prefix dct: <http://purl.org/dc/terms/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?shape ?shapeColor ?shapeLabel {
  ?gemeente a cbs:Gemeente ;
            dct:partOf [] ;
            geo:hasGeometry/geo:asWKT ?shape ;
            rdfs:label ?label .
  graph <https://data.pdok.nl/cbs/energiebesparingspotentie_gemeente> {
    ?gemeente cbs:gemiddeldeTheoretischeBesparingspotentieInEuroPerJaar_populatie ?euro
  }
  bind(xsd:float(?euro - 25) / 1900 as ?normalized)
  bind(concat("jet,",str(?normalized)) as ?shapeColor)
  bind(concat('<h3>',str(?label),'</h3><p>â‚¬',str(?euro),',-</p>') as ?shapeLabel)
}
limit 100
