prefix cbs: <https://data.pdok.nl/cbs/vocab/>
prefix dct: <http://purl.org/dc/terms/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix krimp: <https://data.pdok.nl/cbs/id/krimpgebied/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix wibu: <https://data.pdok.nl/cbs/vocab/>
select ?gemeente ?shape ?shapeLabel ?shapeColor {
  graph <https://data.pdok.nl/cbs/krimpgebieden_2016> {
    ?regio ^dct:partOf ?gemeente .
    filter(?regio = krimp:Oost_Groningen )
  }
  graph<https://data.pdok.nl/cbs/wijken_buurten_2016> {
    ?gemeente wibu:regioaanduiding_Gemeentenaam_naam ?g_naam ;
  }
  graph <https://data.pdok.nl/cbs/gemeente_gegeneraliseerd_2016> {
    ?gemeente geo:hasGeometry/geo:asWKT ?shape .
  }
  graph <https://data.pdok.nl/cbs/arbeidsmarktparticipatie_gemeente> {
    ?gemeente a cbs:Gemeente ;
              wibu:nettoarbeidsparticipatie_p ?g_ap ;
     .
    bind(concat("<h3>Arbeidsparticipatie</h3>",
            "<h4>", ?g_naam, "</h4>",
            "<p>Voor deze gemeente: <b>", str(?g_ap), "%</b></p>" ,
            "<p>Voor Nederland: <b>65.8%</b></p>") as ?shapeLabel)
  }
  values (?regio                      ?shapeColor       ) {
         (krimp:Achterhoek            "orangered"       )
         (krimp:DeMarne               "violet"          )
         (krimp:Eemsdelta             "teal"            )
         (krimp:Maastricht_Mergelland "rebeccapurple"   )
         (krimp:Noord_OostFriesland   "midnightblue"    )
         (krimp:Oost_Groningen        "slateblue"       )
         (krimp:ParkstadLimburg       "mediumvioletred" )
         (krimp:WestelijkeMijnstreek  "olive"           )
         (krimp:Zeeuws_Vlaanderen     "indianred"       ) }
}
