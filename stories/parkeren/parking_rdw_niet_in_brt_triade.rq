PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX lgd: <http://linkedgeodata.org/triplify/>
PREFIX lgdo: <http://linkedgeodata.org/ontology/>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>

SELECT ?parking ?geo ?geoColor
WHERE {
  {
    # Vraag alle RDW parkeergarages op
    service <https://data.labs.pdok.nl/sparql> {
      graph <http://data.labs.pdok.nl/dataset/parkeren> {
        ?parking a lgdo:Parking ;
          geo:asWKT ?geo .
        bind('green' as ?geoColor) .
        filter(geof:sfWithin(?geo, strdt("POLYGON((6.558 52.998, 6.558 52.996, 6.562 52.996, 6.5621 52.9989, 6.558 52.998))", geo:wktLiteral))) .
      }
    }
  } UNION {
    select ?parking ?geo ?geoColor where {
      ?parking a brt:ParkeerdakParkeerdekParkeergarage;
                   geo:hasGeometry/geo:asWKT ?geo .
      bind('red' as ?geoColor) .
      filter(geof:sfIntersects(?geo, strdt("POLYGON((6.558 52.998, 6.558 52.996, 6.562 52.996, 6.5621 52.9989, 6.558 52.998))", geo:wktLiteral))) .
    }
  }
}