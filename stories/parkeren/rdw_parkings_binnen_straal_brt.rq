PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX lgd: <http://linkedgeodata.org/triplify/>
PREFIX lgdo: <http://linkedgeodata.org/ontology/>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>

SELECT ?brt_parking ?rdw_parking
?brt_geo
?rdw_geo
?distance
WHERE {
  {
	  select ?brt_parking ?brt_geo where {
  		?brt_parking a brt:ParkeerdakParkeerdekParkeergarage;
         geo:hasGeometry/geo:asWKT ?brt_geo .
    }
  }
  service <https://data.labs.pdok.nl/sparql> {
    graph <http://data.labs.pdok.nl/dataset/parkeren> {
	    ?rdw_parking a lgdo:Parking;
        	geo:asWKT ?rdw_geo .
    }
  }
  bind(geof:distance(?brt_geo, ?rdw_geo, uom:metre) as ?distance)
  filter(?distance < 50)
}