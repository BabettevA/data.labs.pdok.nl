PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX geosparql: <http://www.opengis.net/ont/geosparql#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
PREFIX brk: <http://brk.basisregistraties.overheid.nl/def/brk#>
select distinct * where {
  {
    select distinct ?nummeraanduiding ?straat ?huisnummer where {
      bind ("Oranjekade"@nl as ?straat)
      ?or_uri rdfs:label ?straat .
      ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?or_uri ;
			  bag:postcode ?postcode ;
        bag:huisnummer ?huisnummer .
      filter(strstarts(?postcode, "2011"))
    }
  }
  service <https://data.labs.pdok.nl/sparql> {
	  ?nummeraanduiding dcterms:related ?perceel
  }
  # Haal de perceelsinformatie op
  ?perceel a brk:Perceel;
           brk:begrenzingPerceel ?geom ;
           brk:perceelnummer ?perceelnummer .
  ?geom geosparql:asWKT ?wkt .
  # Vorm eenvoudige html voor de labelinformatie
  bind(concat(
      "<a target='_blank' href='", str(?nummeraanduiding), "'>", str(?straat), " ", str(?huisnummer), "</a>: <a target='_blank' href='", str(?perceel), "'>", "perceel no. ", str(?perceelnummer), "</a>"
    ) as ?wktLabel)
}