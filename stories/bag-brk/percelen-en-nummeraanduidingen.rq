prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix brk: <http://brk.basisregistraties.overheid.nl/def/brk#>
prefix dct: <http://purl.org/dc/terms/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select distinct * {
  {
    select distinct ?nummeraanduiding ?straat ?huisnummer {
      bind ("Oranjekade"@nl as ?straat)
      ?or_uri rdfs:label ?straat .
      ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?or_uri ;
                        bag:postcode ?postcode ;
                        bag:huisnummer ?huisnummer .
      filter(strstarts(?postcode, "2011"))
    }
  }
  service <https://data.labs.pdok.nl/sparql> {
    # TBD: Dit komt uit de Default Graph.
    ?nummeraanduiding dct:related ?perceel
  }
  # Haal de perceelsinformatie op
  ?perceel a brk:Perceel;
           brk:begrenzingPerceel/geo:asWKT ?wkt ;
           brk:perceelnummer ?perceelnummer .
  # Vorm eenvoudige html voor de labelinformatie
  bind(concat("<a target='_blank' href='", str(?nummeraanduiding), "'>", str(?straat), " ", str(?huisnummer), "</a>: <a target='_blank' href='", str(?perceel), "'>", "perceel no. ", str(?perceelnummer), "</a>") as ?wktLabel)
}
