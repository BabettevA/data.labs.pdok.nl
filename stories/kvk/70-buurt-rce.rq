prefix dbo: <http://dbpedia.org/ontology/>
prefix dcterm: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://data.labs.pdok.nl/graph/>
prefix rce: <https://data.labs.pdok.nl/rce/def/>
prefix vcard: <http://www.w3.org/2006/vcard/ns#>
select ?wkt (min(?label) as ?wktLabel) {
  graph graph:monumenten {
    ?monument dbo:city "Zwolle"@nl-nl ;
              geo:hasGeometry/geo:asWKT ?wkt ;
              rce:bouwjaar ?bouwjaar ;
              rce:huisnummer ?huisnummer ;
              vcard:postal-code ?postcode .
  }
  graph graph:beeldbank {
    ?monument rce:straat ?straat .
    ?image foaf:depicts ?monument ;
           rce:locator ?url .
  }
  bind (concat(
    '<h3>',str(?straat),' ',str(?huisnummer),'</h3>',
    '<p>',if(bound(?bouwjaar),concat('Bouwjaar: ',str(?bouwjaar),'<br>'),''),
    'Postcode: ',str(?postcode),'</p>',
    '<img src="',str(?url),'" style="max-height: 150px;">') as ?label)
}
group by ?wkt
limit 100
