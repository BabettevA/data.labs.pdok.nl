prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix energielabels: <https://data.labs.pdok.nl/def/energielabels/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://data.labs.pdok.nl/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?shape (sample(?shapeColor_) as ?shapeColor) (sample(?shapeLabel_) as ?shapeLabel) {
  {
    select * {
      ?openbareRuimte bag:naamOpenbareRuimte "Diezerstraat" ;
                      bag:bijbehorendeWoonplaats ?woonplaats .
      ?woonplaats bag:naamWoonplaats "Zwolle" .
      ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte ;
                        bag:huisnummer ?huisnummer ;
                        bag:postcode ?postcode .
      ?verblijfsobject bag:hoofdadres ?nummeraanduiding ;
                       bag:oppervlakte ?oppervlakte ;
                       bag:pandrelatering ?pand ;
                       bag:status ?verblijfsobjectStatus .
      ?verblijfsobjectStatus rdfs:label ?verblijfsobjectStatusLabel .
      ?pand bag:oorspronkelijkBouwjaar ?bouwjaar ;
            geo:hasGeometry/geo:asWKT ?shape ;
            bag:status ?pandStatus .
          ?pandStatus rdfs:label ?pandStatusLabel .
    }
  }
  bind(str(?huisnummer) as ?huisnummerString)
  service <https://data.labs.pdok.nl/sparql> {
    graph graph:energielabels {
      optional {
        _:1 energielabels:ep ?ep ;
            energielabels:e_label ?energielabel ;
            energielabels:opname_datum ?opnamedatum ;
            energielabels:pand_huisnummer ?huisnummerString ;
            energielabels:pand_postcode ?postcode ;
            energielabels:registratie_datum ?registratiedatum .
      }
    }
  }
  bind(if(!(bound(?energielabel)), "grey",
        if(strstarts(?energielabel, "A"), "#22b14c",
        if(?energielabel="B", "#8ff334",
        if(?energielabel="C", "#bdfc2c",
        if(?energielabel="D", "#fff200",
        if(?energielabel="E", "#ff9a35",
        if(?energielabel="F", "#ff7f27",
        if(?energielabel="G", "#ed1c24",
        "grey")))))))) as ?shapeColor_)
  bind(concat('<h4><svg height="20" viewBox="0 0 20 20" xmlns="http://www.w3.org/2000/svg"><circle cx="10" cy="10" fill="',?shapeColor_,'" r="10"/></svg> Diezerstraat ',str(?huisnummer),'</h4>',if(bound(?x),concat('<h5>Energielabel informatie:</h5><ul><li>Energielabel: ',str(?energielabel),'</li><li>EP: ',str(?ep),'</li><li>Opnamedatum: ',str(?opnamedatum),'</li><li>Registratiedatum: ',str(?registratiedatum),'</li></ul>'),''),'<h5><a href="',str(?verblijfsobject),'">Verblijfsobject</a>:</h5><ul><li>Woonplaats: <a href="',str(?woonplaats),'">Zwolle</a></li><li>Oppervlakte: ',str(?oppervlakte),'m^2</li><li>Status: <a href="',str(?verblijfsobjectStatus),'">',str(?verblijfsobjectStatusLabel),'</a></li></ul><h5><a href="',str(?pand),'">Pand</a>:</h5><ul><li>Bouwjaar: ',str(?bouwjaar),'</li><li>Status: <a href="',str(?pandStatus),'">',str(?pandStatusLabel),'</a></li></ul>') as ?shapeLabel_)
}
group by ?shape
limit 100
