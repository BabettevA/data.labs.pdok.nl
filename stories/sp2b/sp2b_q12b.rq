# SP²B12b: Return ✓ if an author has published with Paul Erdös or with
# an author that has published with “Paul Erdös”, and ❌ otherwise.
#
# This query shares the properties of its `select' counterpart SP²B8.
# It always returns ✓ for sufficiently large documents.  When
# evaluating such `ask' queries, engines should break as soon a
# solution has been found.  They might adapt the QEP, to efficiently
# locate a witness.  For instance, based on execution time estimations
# it might be favorable to evaluate the second part of the `union' in
# SP²B12b first.  Both native and in-memory engines should answer
# these queries very fast, independent from document size.
#
# |     10,000 | 1 |
# |     50,000 | 1 |
# |    250,000 | 1 |
# |  1,000,000 | 1 |
# |  5,000,000 | 1 |
# | 25,000,000 | 1 |
#
# O(1)

prefix   dc: <http://purl.org/dc/elements/1.1/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix  xsd: <http://www.w3.org/2001/XMLSchema#>
ask {
  ?erdoes a foaf:Person ;
    foaf:name "Paul Erdoes"^^xsd:string .
  {
    ?doc1 dc:creator ?erdoes , ?author1 .
    ?doc2 dc:creator ?author1 , ?author2 .
    ?author2 foaf:name ?name
    filter (
      ?author1 != ?erdoes &&
      ?doc2 != ?doc1 &&
      ?author2 != ?erdoes &&
      ?author2 != ?author1
    )
  } union {
    ?doc1 dc:creator ?erdoes , ?author1 .
    ?author1 foaf:name ?name 
    filter (?author1!=?erdoes)
  }
}
