# SPÂ²B7: Return the titles of all papers that have been cited at least
# once, but not by any paper that has not been cited itself.
#
# This query tests double negation, which requires nested CWN.
# Recalling that the citation system of DBLP is rather incomplete
# (cf. Section III-D), we expect only few results.  Though, the query
# is challenging due to the double negation.  Engines might reuse
# graph pattern results, for instance, the block `?class[i] rdf:type
# foaf:Document. ?doc[i] rdf:type ?class[i].' occurs three times, for
# empty [i], [i]=3, and [i]=4.
#
# |     10,000 |     0 |
# |     50,000 |     2 |
# |    250,000 |    62 |
# |  1,000,000 |   292 |
# |  5,000,000 | 1,200 |
# | 25,000,000 | 5,099 |

prefix   dc: <http://purl.org/dc/elements/1.1/>
prefix  dct: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix  rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select distinct ?title {
  ?class1 rdfs:subClassOf foaf:Document .
  ?doc1 a ?class1 ;
    dc:title ?title .
  ?bag2 ?member2 ?doc1 .
  ?doc2 dct:references ?bag2
  optional {
    ?class3 rdfs:subClassOf foaf:Document .
    ?doc3 a ?class3 ;
      dct:references ?bag3 .
    ?bag3 ?member3 ?doc1
    optional {
      ?class4 rdfs:subClassOf foaf:Document .
      ?doc4 a ?class4 ;
        dct:references ?bag4 .
      ?bag4 ?member4 ?doc3
    } filter (!bound(?doc4))
  } filter (!bound(?doc3))
}
