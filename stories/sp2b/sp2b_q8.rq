# SP²B8: Compute authors that have published with Paul Erdös or with
# an author that has published with Paul Erdös.
#
# Here, the evaluation of the second `union' part is basically
# “contained” in the evaluation of the first part.  Hence, techniques
# like graph pattern (or subexpression) reusing might apply.  Another
# very promising optimization approach is to decompose the filter
# expressions and push down its components, in order to decrease the
# size of intermediate results.
#
# |     10,000 | 184 |
# |     50,000 | 264 |
# |    250,000 | 332 |
# |  1,000,000 | 400 |
# |  5,000,000 | 493 |
# | 25,000,000 | 493 |

prefix   dc: <http://purl.org/dc/elements/1.1/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix  xsd: <http://www.w3.org/2001/XMLSchema#>
select distinct ?name {
  ?erdoes a foaf:Person ;
    foaf:name "Paul Erdoes"^^xsd:string .
  {
    ?doc1 dc:creator ?erdoes , ?author1 .
    ?doc2 dc:creator ?author1 , ?author2 .
    ?author2 foaf:name ?name
    filter (
      ?author!=?erdoes &&
      ?doc2!=?doc1 &&
      ?author2!=?erdoes &&
      ?author2!=?author1
    )
  } union {
    ?doc1 dc:creator ?erdoes , ?author1 .
    ?author1 foaf:name ?name
    filter (?author1!=?erdoes)
  }
}
