# SPÂ²B6: Return, for each year, the set of all publications authored
# by persons that have not published in years before.
#
# This query implements closed world negation (CWN), expressed through
# a combination of operators `optional', `filter', and `bound'.  The
# idea of the construction is that the block outside the `optional'
# expression computes all publications, while the inner one
# constitutes earlier publications from authors that appear outside.
# The outer `filter' expression then retains publications for which
# `?author2' is unbound, i.e. exactly the publications of authors
# without publications in earlier years.
#
# |     10,000 |       229 |
# |     50,000 |     1,769 |
# |    250,000 |    12,093 |
# |  1,000,000 |    62,795 |
# |  5,000,000 |   417,625 |
# | 25,000,000 | 1,945,167 |

prefix   dc: <http://purl.org/dc/elements/1.1/>
prefix  dct: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?yr1 ?name1 ?doc1 {
  ?class1 rdfs:subClassOf foaf:Document .
  ?doc1 a ?class1 ;
    dct:issued ?yr1 ;
    dc:creator ?author1 .
  ?author1 foaf:name ?name1
  optional {
    ?class2 rdfs:subClassOf foaf:Document .
    ?doc2 a ?class2 ;
      dct:issued ?yr2 ;
      dc:creator ?author2 
    filter (?author1=?author2 && ?yr2<?yr1)
  }
  filter (!bound(?author2))
}
