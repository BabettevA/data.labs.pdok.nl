# SPÂ²B4: Select all distinct pairs of article author names for authors
# that have published in the same journal.
#
# This query contains a rather long graph chain, i.e. variables
# `?name1' and `?name2' are linked through the articles the authors
# have published, and a common journal.  The result is very large,
# basically quadratic in number and size of journals.  Instead of
# evaluating the outer pattern block and applying the `filter'
# afterwards, engines might embed the `filter' expression in the
# computation of the block, e.g. by exploiting indices on author
# names.  The `distinct' modifier further complicates the query.  We
# expect superlinear behavior, even for native engines.
#
# |    10,000 |     23,226 |
# |    50,000 |    104,746 |
# |   250,000 |    542,801 |
# | 1,000,000 |  2,586,733 |
# | 5,000,000 | 18,362,955 |
#
# O(superlinear)

prefix bench: <http://localhost/vocabulary/bench/>
prefix    dc: <http://purl.org/dc/elements/1.1/>
prefix  foaf: <http://xmlns.com/foaf/0.1/>
prefix   rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix  swrc: <http://swrc.ontoware.org/ontology#>
select distinct ?name1 ?name2 {
  ?article1 a bench:Article ;
    dc:creator/foaf:name ?name1 ;
    swrc:journal ?journal .
  ?article2 a bench:Article ;
    dc:creator/foaf:name ?name2 ;
    swrc:journal ?journal
  filter (?name1<?name2)
}
