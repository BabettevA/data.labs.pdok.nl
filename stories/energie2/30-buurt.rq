prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix cbs: <https://krr.triply.cc/Kadaster/cbs/def/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix gg: <http://www.gemeentegeschiedenis.nl/gg-schema#>
prefix graph: <https://krr.triply.cc/Kadaster/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?pandWkt ?buurtWkt ?buurtWktLabel {
  bind ("Warnsveld"@nl as ?woonplaatsNaam)
  bind ("'t Spiker" as ?straatNaam)
  bind (24 as ?huisnummer)
  bind (str(?woonplaatsNaam) as ?woonplaatsNaamString)
  service <https://data.pdok.nl/sparql> {
    ?openbareRuimte bag:naamOpenbareRuimte ?straatNaam ;
                    bag:bijbehorendeWoonplaats ?woonplaats .
    ?woonplaats bag:naamWoonplaats ?woonplaatsNaamString .
    ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte ;
                      bag:huisnummer ?huisnummer .
    ?verblijfsobject bag:hoofdadres ?nummeraanduiding ;
                     geo:hasGeometry/geo:asWKT ?punt ;
                     bag:pandrelatering ?pand .
    ?pand bag:oorspronkelijkBouwjaar ?bouwjaar ;
          bag:geometriePand/geo:asWKT ?pandWkt .
  }
  ?oudeGemeente rdfs:label ?woonplaatsNaam ;
                gg:absorbedBy*/gg:cbsCode ?cbsCodeIri .
  bind (IRI(concat('https://krr.triply.cc/Kadaster/cbs/id/gemeente/GM', substr(str(?cbsCodeIri), 44))) as ?gemeente)
  graph graph:cbs-2015 {
    ?buurt geo:sfWithin/geo:sfWithin ?gemeente ;
           geo:hasGeometry/geo:asWKT ?buurtWkt ;
           rdfs:label ?buurtNaam .
  }
  filter (bif:st_intersects(?punt, ?buurtWkt))
  bind (concat('<p>',str(?buurtNaam),'<br>',substr(str(?buurt),45),'</p>') as ?buurtWktLabel)
}
