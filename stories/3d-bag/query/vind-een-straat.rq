prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix def: <https://data.labs.pdok.nl/bag/def/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?pand ?shape ?shapeColor ?shapeHeight ?shapeName ?shapeLabel {
  {
    select
      (sample(?bouwjaar) as ?bouwjaar)
      (group_concat(distinct ?functieLabel;separator=', ') as ?functieLabels)
      (max(?houseNumber) as ?houseNumberMax)
      (min(?houseNumber) as ?houseNumberMin)
      (group_concat(distinct ?huisletter;separator=',') as ?huisletters)
      ?huisnummer
      (group_concat(distinct ?huisnummertoevoeging;separator=',') as ?huisnummertoevoegingen)
      (sum(?oppervlakte) as ?oppervlakte)
      ?pand
      (sample(?shape) as ?shape)
      (sample(?shapeColor) as ?ShapeColor)
      ?shapeHeight
      ?straatNaam
      ?woonplaatsNaam
      (max(?zipcode) as ?zipcodeMax)
      (min(?zipcode) as ?zipcodeMin)
    {
      bind("Almelo"@nl as ?woonplaatsNaam)
      bind("Ootmarsumsestraat"^^xsd:string as ?straatNaam)
      bind(485 as ?huisnummer)
      ?place
        rdfs:isDefinedBy ?graph0;
        rdfs:label ?woonplaatsNaam.
      optional { ?graph0 bag:eindGeldigheid ?eind0. }
      filter(!bound(?eind0))
      ?openbareRuimte
        bag:bijbehorendeWoonplaats ?place;
        bag:naamOpenbareRuimte ?straatNaam;
        rdfs:isDefinedBy ?graph1.
      optional { ?graph1 bag:eindGeldigheid ?eind1. }
      filter(!bound(?eind1))
      ?nummeraanduiding
        bag:bijbehorendeOpenbareRuimte ?openbareRuimte;
        bag:huisnummer ?houseNumber;
        bag:postcode ?zipcode;
        rdfs:isDefinedBy ?graph2.
      optional { ?nummeraanduiding bag:huisletter ?huisletter. }
      optional { ?nummeraanduiding bag:huisnummertoevoeging ?huisnummertoevoeging. }
      optional { ?graph2 bag:eindGeldigheid ?eind2. }
      filter(!bound(?eind2))
      ?verblijfsobject a ?functie;
        bag:hoofdadres ?nummeraanduiding;
        bag:oppervlakte ?oppervlakte;
        bag:pandrelatering ?pand;
        rdfs:isDefinedBy ?graph3.
      optional { ?graph3 bag:eindGeldigheid ?eind3. }
      filter(!bound(?eind3))
      ?functie rdfs:label ?functieLabel.
      filter(?functie!=bag:Verblijfsobject)
      bind(
        if(?functie=bag:VerblijfsobjectWoonfunctie,"green",
        if(?functie=bag:VerblijfsobjectWinkelfunctie,"red",
        if(?functie=bag:VerblijfsobjectKantoorfunctie,"blue",
        if(?functie=bag:VerblijfsobjectOnderwijsfunctie,"yellow",
        if(?functie=bag:VerblijfsobjectGezondheidszorgfunctie,"orange",
        if(?functie=bag:VerblijfsobjectSportfunctie,"violet",
        "orange")))))) as ?shapeColor)
      ?pand
        bag:geometriePand/geo:asWKT ?shape;
        bag:oorspronkelijkBouwjaar ?bouwjaar;
        def:measuredHeight ?x;
        def:minHeightSurface ?y;
        rdfs:isDefinedBy ?graph4.
      bind(?x-?y as ?shapeHeight)
      optional { ?graph4 bag:eindGeldigheid ?eind4. }
      filter(!bound(?eind4))
    }
    group by ?huisnummer ?pand ?shapeHeight ?straatNaam ?woonplaatsNaam
  }
  bind(if((?houseNumberMin<=?huisnummer)&&(?huisnummer<=?houseNumberMax),'red','lightcoral') as ?shapeColor)
  bind(concat(
    str(?houseNumberMin),
    if(?houseNumberMin=?houseNumberMax,
      concat(
        if(bound(?huisletters),?huisletters,''),
        if(bound(?huisnummertoevoegingen),?huisnummertoevoegingen,'')),
      concat(' t/m ',str(?houseNumberMax)))) as ?houseNumberLabel)
  bind(concat(
    str(?zipcodeMin),
    if(?zipcodeMin=?zipcodeMax,'',concat(' t/m ',str(?zipcodeMax)))) as ?zipcodeLabel)
  bind(concat(str(?straatNaam),' ',?houseNumberLabel,', ',?zipcodeLabel,' ',str(?plaatsNaam)) as ?shapeName)
  bind(concat(
    '<h2>',str(?shapeName),'</h2><ul>',
    '<li><b>Oorspronkelijk bouwjaar</b>: ',str(?bouwjaar),'</li>',
    '<li><b>Hoogte</b>: ',str(?shapeHeight),' m</li>',
    '<li><b>Oppervlakte</b>: ',replace(str(?oppervlakte),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),' m^2</li>',
    '<li><b>Gebruiksfuncties</b>: ',?functieLabels,'</li>',
    '</ul>') as ?shapeLabel)
}
limit 500
