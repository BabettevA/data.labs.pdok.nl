prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix def: <https://data.labs.pdok.nl/bag/def/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?shape ?shapeColor ?shapeHeight ?shapeName ?shapeLabel {
  {
    select * {
      bind("Almelo"@nl as ?woonplaatsNaam)
      bind("Ootmarsumsestraat"^^xsd:string as ?straatNaam)
      bind(485 as ?huisnummer)
      service <https://data.pdok.nl/sparql> {
        ?place
          rdfs:isDefinedBy ?graph0;
          rdfs:label ?woonplaatsNaam.
        optional { ?graph0 bag:eindGeldigheid ?eind0. }
        filter(!bound(?eind0))
        ?openbareRuimte
          bag:bijbehorendeWoonplaats ?place;
          bag:naamOpenbareRuimte ?straatNaam;
          rdfs:isDefinedBy ?graph1.
        optional { ?graph1 bag:eindGeldigheid ?eind1. }
        filter(!bound(?eind1))
        ?nummeraanduiding
          bag:bijbehorendeOpenbareRuimte ?openbareRuimte;
          bag:huisnummer ?huisnummer;
          bag:postcode ?zipcode;
          rdfs:isDefinedBy ?graph2.
        optional { ?graph2 bag:eindGeldigheid ?eind2. }
        filter(!bound(?eind2))
        ?verblijfsobject a ?c;
          bag:hoofdadres ?nummeraanduiding;
          bag:oppervlakte ?oppervlakte;
          bag:pandrelatering ?pand;
          rdfs:isDefinedBy ?graph3.
        optional { ?graph3 bag:eindGeldigheid ?eind3. }
        filter(!bound(?eind3))
        ?pand
          bag:geometriePand/geo:asWKT ?shape;
          bag:oorspronkelijkBouwjaar ?bouwjaar;
          rdfs:isDefinedBy ?graph4.
        optional { ?graph4 bag:eindGeldigheid ?eind4. }
        filter(!bound(?eind4))
      }
    }
  }
  filter(?c!=bag:Verblijfsobject)
  bind(if(?c=bag:VerblijfsobjectWoonfunctie,"green",
       if(?c=bag:VerblijfsobjectWinkelfunctie,"red",
       if(?c=bag:VerblijfsobjectKantoorfunctie,"blue",
       if(?c=bag:VerblijfsobjectOnderwijsfunctie,"yellow",
       if(?c=bag:VerblijfsobjectGezondheidszorgfunctie,"orange",
       if(?c=bag:VerblijfsobjectSportfunctie,"violet",
       "orange")))))) as ?shapeColor)
  ?pand
    def:measuredHeight ?x;
    def:minHeightSurface ?y.
  bind(?x-?y as ?shapeHeight)
  ?c rdfs:label ?cLabel.
  bind(concat(str(?straatNaam),' ',str(?huisnummer),', ',str(?zipcode),' ',str(?woonplaatsNaam)) as ?shapeName)
  bind(concat(
    '<h2>',str(?shapeName),'</h2><ul>',
    '<li><b>Oorspronkelijk bouwjaar</b>: ',str(?bouwjaar),'</li>',
    '<li><b>Maximale hoogte</b>: ',str(?shapeHeight),' m</li>',
    '<li><b>Oppervlakte</b>: ',replace(str(?oppervlakte),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),' mÂ²</li>',
    '</ul>') as ?shapeLabel)
}
limit 5
