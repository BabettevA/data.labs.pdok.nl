prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix geof: <http://www.opengis.net/def/function/geosparql/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix uom: <http://www.opengis.net/def/uom/OGC/1.0/>
prefix verblijfsobject: <http://bag.basisregistraties.overheid.nl/bag/id/verblijfsobject/>
select ?vboWkt ?vboWktLabel {
  {
    ?vbo a ?type ;
         bag:pandrelatering ?pand ;
         geo:hasGeometry/geo:asWKT ?vboWkt ;
         rdfs:label ?vboLabel .
    ?pand rdfs:label ?pandLabel .
    filter(?vbo = verblijfsobject:0307010000462567)
    bind(concat(
      '<h3>Coffeeshop</h3>',
      '<p><a href="',str(?pand),'">',str(?pandLabel),'</a><br>',
      '<a href="',str(?vbo),'">',str(?vboLabel),'</a></p>'
      ) as ?vboWktLabel)
  } union {
    ?vbo a ?type ;
         bag:pandrelatering ?pand ;
         geo:hasGeometry/geo:asWKT ?vboWkt ;
         rdfs:label ?vboLabel .
    ?pand rdfs:label ?pandLabel .
    filter(?vbo = verblijfsobject:0307010000519401)
    bind(concat(
      '<h3>School</h3>',
      '<p><a href="',str(?pand),'">',str(?pandLabel),'</a><br>',
      '<a href="',str(?vbo),'">',str(?vboLabel),'</a></p>'
      ) as ?vboWktLabel)
  } union {
    verblijfsobject:0307010000462567 geo:hasGeometry/geo:asWKT ?punt1 .
    verblijfsobject:0307010000519401 geo:hasGeometry/geo:asWKT ?punt2 .
    bind(concat('LineString ((',replace(str(?punt1),'POINT\\s\\((.*)\\)','$1'),', ',replace(str(?punt2),'Point\\s\\((.*)\\)','$1'),'))') as ?vboWkt)
    bind(geof:distance(?punt1, ?punt2, uom:metre) as ?afstand)
    bind(concat('<h3>Afstand hemelsbreed</h3><br>',str(round(?afstand)),' meter') as ?vboWktLabel)
  }
}
