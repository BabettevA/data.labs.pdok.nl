prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix begrip: <http://bag.basisregistraties.overheid.nl/id/begrip/>
prefix bgt: <http://bgt.basisregistraties.overheid.nl/def/bgt#>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select distinct ?wkt ?wktColor ?wktLabel {
  {
    select ?bgt ?bag ?begin ?eind {
      ?bgt a bgt:Pand ;
           foaf:isPrimaryTopicOf ?voorkomen ;
           bgt:gerelateerdBagPand ?bag .
      ?voorkomen bgt:objectBegintijd ?begin ;
                 bgt:objectEindtijd ?eind .
      filter(year(?eind) <= 2017)
    }
    limit 100
  }
  service <https://data.pdok.nl/sparql> {
    graph ?voorkomen {
      ?bag bag:status ?status .
      filter(?status = begrip:PandInGebruik_nietIngemeten || ?status = begrip:PandGesloopt)
    }
    optional {
      ?voorkomen bag:eindGeldigheid ?eind0 .
    }
    ?openbareRuimte bag:naamOpenbareRuimte ?openbareRuimteLabel ;
                  bag:bijbehorendeWoonplaats/rdfs:label ?woonplaatsLabel .
    ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte ;
                      bag:huisnummer ?huisnummer ;
                      bag:postcode ?postcode .
    ?verblijfsobject bag:hoofdadres ?nummeraanduiding ;
                     bag:oppervlakte ?oppervlakte ;
                     bag:pandrelatering ?bag .
    ?bag bag:geometriePand/geo:asWKT ?wkt ;
         bag:oorspronkelijkBouwjaar ?bouwjaar .
    ?status rdfs:label ?statusLabel .
  }
  filter(!(bound(?eind0)))
  bind(if(year(?eind) < 2017,"red",if(month(?eind) < 7,"red","orange")) as ?wktColor)
  bind(concat(str(year(?begin)),'-',str(month(?begin)),'-',str(day(?begin))) as ?beginLabel)
  bind(concat(str(year(?eind)),'-',str(month(?eind)),'-',str(day(?eind))) as ?eindLabel)
  bind(concat('<h3>',str(?openbareRuimteLabel),' ',str(?huisnummer),'<br>',str(?postcode),' ',str(?woonplaatsLabel),'</h3><h4><a href="',str(?bag),'">BAG</a></h4><p>Status: ',str(?statusLabel),'<br>Oppervlakte: ',str(?oppervlakte),'m^2<br>Bouwjaar: ',str(?bouwjaar),'</p><h4><a href="',str(?bgt),'">BGT</a></h4><p>Begintijd: ',str(?beginLabel),'<br>Eindtijd: ',str(?eindLabel),'</p>') as ?wktLabel)
}
order by ?delta
limit 200
