prefix brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
prefix cbs: <https://data.pdok.nl/cbs/def/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?wkt ?wktLabel ?wktColor ?cafeBuurt ?green ?red {
  service <https://data.pdok.nl/sparql> {
    bind ("Apeldoorn" as ?woonplaatsNaam)
    ?registratiefGebied brt:naamOfficieel ?woonplaatsNaam ;
                        brt:nummer ?registratiefGebiedCode
  }
  graph <https://data.pdok.nl/cbs/2015> {
    bind (concat("GM",?registratiefGebiedCode) as ?gemeenteCode)
    ?buurt cbs:GM_CODE ?gemeenteCode ;
           cbs:BU_CODE ?buurtCode ;
           cbs:WK_CODE ?wijkCode ;
           geo:hasGeometry/geo:asWKT ?wkt ;
           cbs:AF_CAFE ?cafeBuurt ;
           cbs:BU_NAAM ?buurtNaam .
    bind (concat(
      '<p><b><a href="',str(?buurt),'">',str(?buurtNaam),'</a></b> (',str(?buurtCode),')<br>',
      str(?cafeBuurt),' km tot caf√©</p>'
      ) as ?wktLabel)
    bind (xsd:integer(xsd:float(?cafeBuurt)) * 50 as ?green)
    bind (255 - (xsd:integer(xsd:float(?cafeBuurt)) * 50) as ?red)
    bind (concat('rgb(',str(?red),',0,',str(?green),')') as ?wktColor)
  }
}
limit 100
