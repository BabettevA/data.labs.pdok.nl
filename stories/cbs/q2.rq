prefix def: <http://betalinkeddata.cbs.nl/def/83487NED#>
prefix dimension: <http://betalinkeddata.cbs.nl/def/dimension#>
prefix land: <http://betalinkeddata.cbs.nl/regios/2016/id/land-geografisch/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix wijk: <http://betalinkeddata.cbs.nl/regios/2016/id/wijk/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?regioLabel ?inwoners0_14 ?inwoners15_24 ?inwoners25_44 ?inwoners45_64 ?inwoners65plus {
  values ?regio { land:NL00 wijk:WK063709 wijk:WK063702 wijk:WK063700 wijk:WK063708 wijk:WK063701 wijk:WK063704 wijk:WK063706 wijk:WK063705 wijk:WK063703 }
  [ def:bevolking_AantalInwoners ?totaal;
    dimension:regio ?regio ].
  [ def:bevolking_Leeftijdsgroepen_0Tot15Jaar ?x1;
    dimension:regio ?regio ].
  [ def:bevolking_Leeftijdsgroepen_15Tot25Jaar ?x2;
    dimension:regio ?regio ].
  [ def:bevolking_Leeftijdsgroepen_25Tot45Jaar ?x3;
    dimension:regio ?regio ].
  [ def:bevolking_Leeftijdsgroepen_45Tot65Jaar ?x4;
    dimension:regio ?regio ].
  [ def:bevolking_Leeftijdsgroepen_65JaarOfOuder ?x5;
    dimension:regio ?regio ].
  bind(xsd:int(?x1/xsd:double(?totaal)*1.0e2) as ?inwoners0_14)
  bind(xsd:int(?x2/xsd:double(?totaal)*1.0e2) as ?inwoners15_24)
  bind(xsd:int(?x3/xsd:double(?totaal)*1.0e2) as ?inwoners25_44)
  bind(xsd:int(?x4/xsd:double(?totaal)*1.0e2) as ?inwoners45_64)
  bind(xsd:int(?x5/xsd:double(?totaal)*1.0e2) as ?inwoners65plus)
  ?regio rdfs:label ?regioLabel.
}
