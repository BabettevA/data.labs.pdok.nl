prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
prefix dct: <http://purl.org/dc/terms/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix geof: <http://www.opengis.net/def/function/geosparql/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?shape ?shapeLabel {
  values (?woonplaatsNaam ?straatNaam          ?huisnummer) {
         ('Apeldoorn'@nl  'Laan van Westenenk' 701        ) }
  bind(str(?woonplaatsNaam) as ?bagWplNaam) .
  ?openbareRuimte bag:bijbehorendeWoonplaats ?woonplaats ;
                  bag:naamOpenbareRuimte ?straatNaam ;
                  bag:status ?openbareRuimteStatus .
  ?openbareRuimteStatus skos:prefLabel ?openbareRuimteStatusLabel .
  ?woonplaats bag:identificatiecode ?woonplaatsCode ;
              bag:naamWoonplaats ?bagWplNaam .
  ?registratiefGebied brt:naamOfficieel ?woonplaatsNaam ;
                      brt:nummer ?registratiefGebiedCode .
  ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte ;
                    bag:huisnummer ?huisnummer ;
                    bag:postcode ?postcode .
  ?verblijfsobject bag:hoofdadres ?nummeraanduiding ;
                   bag:oppervlakte ?oppervlakte ;
                   bag:pandrelatering ?pand ;
                   bag:status ?verblijfsobjectStatus ;
                   geo:hasGeometry/geo:asWKT ?punt ;
                   a ?verblijfsobjectKlasse ; # Bring this to the top to destroy performance :-)
                   rdfs:isDefinedBy ?voorkomen .
  filter (?verblijfsobjectKlasse != bag:Verblijfsobject)
  ?voorkomen bag:beginGeldigheid ?begin .
  filter not exists { ?voorkomen bag:eindGeldigheid [] }
  ?verblijfsobjectKlasse dct:subject/skos:prefLabel ?verblijfsobjectKlasseLabel .
  ?verblijfsobjectStatus skos:prefLabel ?verblijfsobjectStatusLabel .
  ?pand bag:oorspronkelijkBouwjaar ?buildyear ;
        bag:geometriePand/geo:asWKT ?shape ;
        bag:status ?pandStatus .
  ?pandStatus skos:prefLabel ?pandStatusLabel
  bind (month(?begin) as ?maand)
  bind (if(?maand=1,'januari',
        if(?maand=2,'februari',
        if(?maand=3,'maart',
        if(?maand=4,'april',
        if(?maand=5,'mei',
        if(?maand=6,'juni',
        if(?maand=7,'juli',
        if(?maand=8,'augustus',
        if(?maand=9,'september',
        if(?maand=10,'oktober',
        if(?maand=11,'november',
        'december'))))))))))) as ?maandLabel)
  bind (concat(
    '<p><b><a href="',str(?openbareRuimte),'">',str(?straatNaam),'</a> ',str(?huisnummer),'</b><br>',
    '<a href="',str(?nummeraanduiding),'">',str(?postcode),'</a> <a href="',str(?woonplaats),'">',str(?woonplaatsNaam),'</a> (',str(?woonplaatsCode),')</p>',
    '<p><b><a href="',str(?pand),'">Pand</a></b><br>',
    'Bouwjaar: ',str(?buildyear),'<br>',
    'Status: <a href="',str(?pandStatus),'">',str(?pandStatusLabel),'</a></p>',
    '<p><b><a href="',str(?verblijfsobject),'">Verblijfsobject</a></b><br>',
    'Geldig vanaf: ',str(day(?begin)),' ',str(?maandLabel),' ',str(year(?begin)),'<br>',
    'Oppervlakte: ',str(?oppervlakte),'mÂ²<br>',
    'Status: <a href="',str(?verblijfsobjectStatus),'">',str(?verblijfsobjectStatusLabel),'</a><br>',
    'Type: <a href="',str(?verblijfsobjectKlasse),'">',str(?verblijfsobjectKlasseLabel),'</a></p>',
    '<p><b><a href="',str(?openbareRuimte),'">Openbare ruimte</a></b><br>',
    'Status: <a href="',str(?openbareRuimteStatus),'">',str(?openbareRuimteStatusLabel),'</a></p>',
    '<p>Registratief gebied (BRT)<br>',
    '<a href="',str(?registratiefGebied),'">',str(?woonplaatsNaam),'</a> (',str(?registratiefGebiedCode),')</p>'
    ) as ?shapeLabel)
}
limit 10
