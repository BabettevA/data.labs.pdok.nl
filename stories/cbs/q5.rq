prefix def: <http://betalinkeddata.cbs.nl/def/83487NED#>
prefix dimension: <http://betalinkeddata.cbs.nl/def/dimension#>
prefix gemeente: <http://betalinkeddata.cbs.nl/regios/2016/id/gemeente-geografisch/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select * {
  ?buurt
    geo:hasGeometry/geo:asWKT ?shape;
    rdfs:label ?buurtLabel;
    # HACK: Work around GraphDB bug.
    ?blank1 _:102.
   _:102 ?blank2 gemeente:GM0637.
  {
    select (min(?afstand) as ?min) (max(?afstand) as ?max) {
      [ def:nabijheidVoorzieningen_AfstandTotGroteSupermarkt ?supermarkt;
        dimension:regio ?buurt ].
      [ def:nabijheidVoorzieningen_AfstandTotHuisartsenpraktijk ?huisarts;
        dimension:regio ?buurt ].
      [ def:nabijheidVoorzieningen_AfstandTotKinderdagverblijf ?kinderdagverblijf;
        dimension:regio ?buurt ].
      [ def:nabijheidVoorzieningen_Basisonderwijs_AfstandTotSchool ?school;
        dimension:regio ?buurt ].
      bind(?supermarkt+?huisarts+?kinderdagverblijf+?school as ?afstand)
    }
  }
  [ def:nabijheidVoorzieningen_AfstandTotGroteSupermarkt ?supermarkt;
      dimension:regio ?buurt ].
  [ def:nabijheidVoorzieningen_AfstandTotHuisartsenpraktijk ?huisarts;
      dimension:regio ?buurt ].
  [ def:nabijheidVoorzieningen_AfstandTotKinderdagverblijf ?kinderdagverblijf;
      dimension:regio ?buurt ].
  [ def:nabijheidVoorzieningen_Basisonderwijs_AfstandTotSchool ?school;
      dimension:regio ?buurt ].
  bind(?supermarkt+?huisarts+?kinderdagverblijf+?school as ?afstand)
  bind((?afstand*4-?min)/xsd:double(?max-?min) as ?score)
  bind(concat('jet,',str(?score)) as ?shapeColor)
  bind('''
       <h3>Zoetermeer {{buurtLabel}}</h3>
       <dl>
         <dt>Afstand tot supermark</dt>
         <dd>{{supermarkt}} km</dd>
         <dt>Afstand tot huisarts</dt>
         <dd>{{huisarts}} km</dd>
         <dt>Afstand tot kinderopvang<dt>
         <dd>{{kinderdagverblijf}} km</dd>
         <dt>Afstand tot school</dt>
         <dd>{{school}} km</dd>
       </dl>''' as ?shapeLabel)
}
