prefix def: <http://betalinkeddata.cbs.nl/def/83487NED#>
prefix dimension: <http://betalinkeddata.cbs.nl/def/dimension#>
prefix gemeente: <http://betalinkeddata.cbs.nl/regios/2016/id/gemeente-geografisch/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?shape ?shapeColor ?shapeLabel {
  {
    _:1 def:wonen_Woningvoorraad ?aantal ;
        dimension:regio ?buurt .
    filter not exists {
      _:2 def:wonen_GemiddeldeWoningwaarde ?waarde ;
          dimension:regio ?buurt .
    }
  } union {
    {
      select (min(?waarde) as ?min) (max(?waarde) as ?max) {
        _:3 def:wonen_GemiddeldeWoningwaarde ?waarde ;
            dimension:regio/geo:sfWithin/geo:sfWithin gemeente:GM0637
      }
    }
    _:4 def:wonen_GemiddeldeWoningwaarde ?waarde ;
        dimension:regio ?buurt .
    _:5 def:wonen_Woningvoorraad ?aantal ;
        dimension:regio ?buurt .
    bind(concat('jet,',str((?waarde-?min)/xsd:double(?max-?min))) as ?shapeColor)
  }
  ?buurt geo:hasGeometry/geo:asWKT ?shape ;
         geo:sfWithin/geo:sfWithin gemeente:GM0637 ;
         rdfs:label ?buurtLabel
  bind(concat('<h3>Zoetermeer „Äã ',str(?buurtLabel),'</h3>',
              '<h4>üè† Gem. woningwaarde: ',if(bound(?waarde),concat('‚Ç¨ ',str(?waarde),'.000,-'),'onbekend'),'</h4>',
              '<h4>üèô Woning voorraad: ',replace(?aantal,'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),'</h4>') as ?shapeLabel)
}
