prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
prefix cbs: <http://betalinkeddata.cbs.nl/def/cbs#>
prefix cbs-qb: <http://betalinkeddata.cbs.nl/def/cbs-qb#>
prefix dct: <http://purl.org/dc/terms/>
prefix def: <http://betalinkeddata.cbs.nl/def/83487NED#>
prefix dimension: <http://betalinkeddata.cbs.nl/def/dimension#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix geof: <http://www.opengis.net/def/function/geosparql/>
prefix graph: <https://data.labs.pdok.nl/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix slice: <http://betalinkeddata.cbs.nl/83487NED/id/slice/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?buurtShape ?buurtShapeLabel ?pandShape ?pandShapeLabel {
  {
    select ?gemeenteCode ?pandShape ?pandShapeLabel ?verblijfsobjectShape {
      values (?woonplaatsNaam ?straatNaam          ?huisnummer) {
             ('Apeldoorn'@nl  'Laan van Westenenk' 701        ) }
      bind(str(?woonplaatsNaam) as ?woonplaatsNaamString) .
      ?woonplaats bag:identificatiecode ?woonplaatsCode ;
                  bag:naamWoonplaats ?woonplaatsNaamString .
      ?openbareRuimte bag:bijbehorendeWoonplaats ?woonplaats ;
                      bag:naamOpenbareRuimte ?straatNaam ;
                      bag:status ?openbareRuimteStatus .
      ?openbareRuimteStatus skos:prefLabel ?openbareRuimteStatusLabel .
      ?registratiefGebied brt:naamOfficieel ?woonplaatsNaam ;
                          brt:nummer ?registratiefGebiedCode .
      ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte ;
                        bag:huisnummer ?huisnummer ;
                        bag:postcode ?postcode .
      ?verblijfsobject a ?verblijfsobjectKlasse ;
                       bag:hoofdadres ?nummeraanduiding ;
                       bag:oppervlakte ?oppervlakte ;
                       bag:pandrelatering ?pand ;
                       bag:status ?verblijfsobjectStatus ;
                       geo:hasGeometry/geo:asWKT ?verblijfsobjectShape ;
                       rdfs:isDefinedBy ?voorkomen .
      filter(?verblijfsobjectKlasse != bag:Verblijfsobject)
      ?voorkomen bag:beginGeldigheid ?begin .
      filter not exists { ?voorkomen bag:eindGeldigheid [] }
      ?verblijfsobjectKlasse dct:subject/skos:prefLabel ?verblijfsobjectKlasseLabel .
      ?verblijfsobjectStatus skos:prefLabel ?verblijfsobjectStatusLabel .
      ?pand bag:geometriePand/geo:asWKT ?pandShape ;
            bag:oorspronkelijkBouwjaar ?bouwjaar ;
            bag:status ?pandStatus .
      ?pandStatus skos:prefLabel ?pandStatusLabel
      bind(concat('GM',str(?registratiefGebiedCode)) as ?gemeenteCode)
      bind(month(?begin) as ?maand)
      bind(if(?maand=1,'januari',
           if(?maand=2,'februari',
           if(?maand=3,'maart',
           if(?maand=4,'april',
           if(?maand=5,'mei',
           if(?maand=6,'juni',
           if(?maand=7,'juli',
           if(?maand=8,'augustus',
           if(?maand=9,'september',
           if(?maand=10,'oktober',
           if(?maand=11,'november',
           'december'))))))))))) as ?maandLabel)
      bind(concat(
        '<p><b><a href="',str(?openbareRuimte),'">',str(?straatNaam),'</a> ',str(?huisnummer),'</b><br>',
        '<a href="',str(?nummeraanduiding),'">',str(?postcode),'</a> <a href="',str(?woonplaats),'">',str(?woonplaatsNaam),'</a> (',str(?woonplaatsCode),')</p>',
        '<p><b><a href="',str(?pand),'">Pand</a></b><br>',
        'Bouwjaar: ',str(?bouwjaar),'<br>',
        'Status: <a href="',str(?pandStatus),'">',str(?pandStatusLabel),'</a></p>',
        '<p><b><a href="',str(?verblijfsobject),'">Verblijfsobject</a></b><br>',
        'Geldig vanaf: ',str(day(?begin)),' ',str(?maandLabel),' ',str(year(?begin)),'<br>',
        'Oppervlakte: ',str(?oppervlakte),'m²<br>',
        'Status: <a href="',str(?verblijfsobjectStatus),'">',str(?verblijfsobjectStatusLabel),'</a><br>',
        'Type: <a href="',str(?verblijfsobjectKlasse),'">',str(?verblijfsobjectKlasseLabel),'</a></p>',
        '<p><b><a href="',str(?openbareRuimte),'">Openbare ruimte</a></b><br>',
        'Status: <a href="',str(?openbareRuimteStatus),'">',str(?openbareRuimteStatusLabel),'</a></p>',
        '<p>Registratief gebied (BRT)<br>',
        '<a href="',str(?registratiefGebied),'">',str(?woonplaatsNaam),'</a> (',str(?registratiefGebiedCode),')</p>'
        ) as ?pandShapeLabel)
    }
  }
  service <https://data.acceptatie.pdok.nl/sparql/cbs> {
    ?observation cbs-qb:inObservationGroup slice:nabijheidVoorzieningen_AfstandTotHuisartsenpraktijk ;
                 def:nabijheidVoorzieningen_AfstandTotHuisartsenpraktijk ?afstand ;
                 dimension:regio ?buurt ;
                 rdfs:label ?buurtNaam .
#cbs:bevolkingsdichtheid ?bevolkingsDichtheid ;
#cbs:gehuwd ?gehuwd ;
#cbs:ongehuwd ?ongehuwd ;
#cbs:verweduwd ?verweduwd ;
#cbs:zwembad ?afstandZwembad ;
    ?buurt geo:hasGeometry/geo:asWKT ?buurtShape ;
           geo:sfWithin/geo:sfWithin/cbs:regioCode ?gemeenteCode .
    filter(geof:sfWithin(?verblijfsobjectShape,?buurtShape))
    bind(concat(
      '<p><b>Statistieken voor buurt ',str(?buurtNaam),'</b></p>',
      #'<p>Bevolkingsdichtheid: ',str(?bevolkingsDichtheid),' inwoners per km²<br>',
      #'Afstand tot zwembad: ',str(?afstandZwembad),' km<br>',
      #'Gehuwd: ',str(?gehuwd),' personen<br>',
      #'Ongehuwd: ',str(?ongehuwd),' personen<br>',
      'Afstand tot café: ',str(?afstand),' km</p>') as ?buurtShapeLabel)
    }
  }
}
limit 1
