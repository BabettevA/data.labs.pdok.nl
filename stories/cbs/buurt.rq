prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
prefix cbs: <https://data.pdok.nl/cbs/def/>
prefix dct: <http://purl.org/dc/terms/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix geof: <http://www.opengis.net/def/function/geosparql/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?wkt ?wktLabel ?oppervlakte ?oppervlakteLabel {
  bind ("Apeldoorn" as ?woonplaatsNaam)
  bind ("Laan van Westenenk" as ?straatNaam)
  bind (701 as ?huisnummer)
  service <https://data.pdok.nl/sparql> {
    bind ("Apeldoorn" as ?woonplaatsNaam)
    bind ("Laan van Westenenk" as ?straatNaam)
    bind (701 as ?huisnummer)
    ?woonplaats bag:identificatiecode ?woonplaatsCode ;
                bag:naamWoonplaats ?woonplaatsNaam .
    ?openbareRuimte bag:bijbehorendeWoonplaats ?woonplaats ;
                    bag:naamOpenbareRuimte ?straatNaam ;
                    bag:status ?openbareRuimteStatus .
    ?openbareRuimteStatus skos:prefLabel ?openbareRuimteStatusLabel .
    ?registratiefGebied brt:naamOfficieel ?woonplaatsNaam ;
                        brt:nummer ?registratiefGebiedCode .
    ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte ;
                      bag:huisnummer ?huisnummer ;
                      bag:postcode ?postcode .
    ?verblijfsobject bag:hoofdadres ?nummeraanduiding ;
                     bag:oppervlakte ?surface ;
                     bag:pandrelatering ?pand ;
                     bag:status ?verblijfsobjectStatus ;
                     geo:hasGeometry/geo:asWKT ?punt ;
                     a ?verblijfsobjectKlasse ; # Bring this to the top to destroy performance :-)
                     rdfs:isDefinedBy ?voorkomen .
    filter (?verblijfsobjectKlasse != bag:Verblijfsobject)
    ?voorkomen bag:beginGeldigheid ?begin .
    filter not exists { ?voorkomen bag:eindGeldigheid [] }
    ?verblijfsobjectKlasse dct:subject/skos:prefLabel ?verblijfsobjectKlasseLabel .
    ?verblijfsobjectStatus skos:prefLabel ?verblijfsobjectStatusLabel .
    ?pand bag:oorspronkelijkBouwjaar ?buildyear ;
          bag:geometriePand/geo:asWKT ?wkt ;
          bag:status ?pandStatus .
    ?pandStatus skos:prefLabel ?pandStatusLabel
  }
  graph <https://data.pdok.nl/cbs/2015> {
    bind (concat("GM",?registratiefGebiedCode) as ?gemeenteCode)
    ?buurt cbs:GM_CODE ?gemeenteCode ;
           cbs:BU_CODE ?buurtCode ;
           cbs:WK_CODE ?wijkCode ;
           geo:hasGeometry/geo:asWKT ?oppervlakte ;
           cbs:BU_NAAM ?buurtNaam .
    filter (geof:sfWithin(?punt, ?oppervlakte) = true)
    ?wijk cbs:WK_CODE ?wijkCode ;
          cbs:WK_NAAM ?wijkNaam .
    filter not exists { ?wijk cbs:BU_CODE [] }
    ?gemeente cbs:GM_CODE ?gemeenteCode ;
              cbs:GM_NAAM ?gemeenteNaam .
    filter not exists { ?gemeente cbs:WK_CODE [] }
    ?buurt cbs:AF_CAFE ?cafeBuurt .
    ?wijk cbs:AF_CAFE ?cafeWijk .
    ?gemeente cbs:AF_CAFE ?cafeGemeente .
  }
  bind (concat(
    '<p><b>Afstand tot dichtstbijzijnde café</b></p>',
    '<p>Buurt <a href="',str(?buurt),'">',str(?buurtNaam),'</a> (',str(?buurtCode),'): ',str(?cafeBuurt),' km</p>',
    '<p>Wijk <a href="',str(?wijk),'">',str(?wijkNaam),'</a> (',str(?wijkCode),'): ',str(?cafeWijk),' km</p>',
    '<p>Gemeente <a href="',str(?gemeente),'">',str(?gemeenteNaam),'</a> (',str(?gemeenteCode),'): ',str(?cafeGemeente),' km</p>'
    ) as ?oppervlakteLabel)
  bind (month(?begin) as ?maand)
  bind (if(?maand=1,"januari",
        if(?maand=2,"februari",
        if(?maand=3,"maart",
        if(?maand=4,"april",
        if(?maand=5,"mei",
        if(?maand=6,"juni",
        if(?maand=7,"juli",
        if(?maand=8,"augustus",
        if(?maand=9,"september",
        if(?maand=10,"oktober",
        if(?maand=11,"november",
        "december"))))))))))) as ?maandLabel)
  bind (concat(
    '<p><b><a href="',str(?openbareRuimte),'">',str(?straatNaam),'</a> ',str(?huisnummer),'</b><br>',
    '<a href="',str(?nummeraanduiding),'">',str(?postcode),'</a> <a href="',str(?woonplaats),'">',str(?woonplaatsNaam),'</a> (',str(?woonplaatsCode),')</p>',
    '<p><b><a href="',str(?pand),'">Pand</a></b><br>',
    'Bouwjaar: ',str(?buildyear),'<br>',
    'Status: <a href="',str(?pandStatus),'">',str(?pandStatusLabel),'</a></p>',
    '<p><b><a href="',str(?verblijfsobject),'">Verblijfsobject</a></b><br>',
    'Geldig vanaf: ',str(day(?begin)),' ',str(?maandLabel),' ',str(year(?begin)),'<br>',
    'Oppervlakte: ',str(?surface),'m²<br>',
    'Status: <a href="',str(?verblijfsobjectStatus),'">',str(?verblijfsobjectStatusLabel),'</a><br>',
    'Type: <a href="',str(?verblijfsobjectKlasse),'">',str(?verblijfsobjectKlasseLabel),'</a></p>',
    '<p><b><a href="',str(?openbareRuimte),'">Openbare ruimte</a></b><br>',
    'Status: <a href="',str(?openbareRuimteStatus),'">',str(?openbareRuimteStatusLabel),'</a></p>',
    '<p>Registratief gebied (BRT)<br>',
    '<a href="',str(?registratiefGebied),'">',str(?woonplaatsNaam),'</a> (',str(?registratiefGebiedCode),')</p>'
    ) as ?wktLabel)
}
limit 10
