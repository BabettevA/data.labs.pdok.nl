prefix brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
prefix cbs: <https://krr.triply.cc/Kadaster/cbs/def/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://krr.triply.cc/Kadaster/graph/>
select (max(?min) as ?max) {
  graph graph:cbs-2015 {
    ?gemeente a cbs:Gemeente .
  }
  {
    select ?gemeente (min(?d) as ?min)  {
      graph graph:cbs-2015 {
        ?gemeente a cbs:Gemeente ;
                  geo:hasGeometry/geo:asWKT ?wktA .
      }
      graph graph:brt {
        ?feature a brt:Kunstijsbaan ;
                 geo:hasGeometry/geo:asWKT ?wktB .
      }
      bind (bif:ST_XMin(?wktA) as ?Ax1)
      bind (bif:ST_YMin(?wktA) as ?Ay1)
      bind (bif:ST_XMax(?wktA) as ?Ax2)
      bind (bif:ST_YMax(?wktA) as ?Ay2)
      bind (bif:st_point(?Ax1 + ((?Ax2 - ?Ax1) / 2), ?Ay1 + ((?Ay2 - ?Ay1) / 2)) as ?pA)
      bind (bif:ST_XMin(?wktB) as ?Bx1)
      bind (bif:ST_YMin(?wktB) as ?By1)
      bind (bif:ST_XMax(?wktB) as ?Bx2)
      bind (bif:ST_YMax(?wktB) as ?By2)
      bind (bif:st_point(?Bx1 + ((?Bx2 - ?Bx1) / 2), ?By1 + ((?By2 - ?By1) / 2)) as ?pB)
      bind (bif:st_distance(?pA, ?pB) as ?d)
    }
    group by ?gemeente
  }
}
