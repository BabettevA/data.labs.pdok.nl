prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix pand: <http://bag.basisregistraties.overheid.nl/bag/id/pand/>
prefix verblijfsobject: <http://bag.basisregistraties.overheid.nl/bag/id/verblijfsobject/>
select distinct ?wkt0 ?wkt0Color ?wkt0Label ?wkt ?wktLabel {
  bind (pand:0010100000008706 as ?pand)
  service <https://data.pdok.nl/sparql> {
    verblijfsobject:0010010000050378 geo:hasGeometry/geo:asWKT ?p0 .
    ?pand bag:geometriePand/geo:asWKT ?wkt0 .
  }
  bind (concat('<p><a href="',?pand,'">BAG</a></p>') as ?wkt0Label)
  bind ("red" as ?wkt0Color)
  graph <https://krr.triply.cc/LaurensRietveld/brt/graphs/default> {
    ?kerncentrale a brt:KerncentraleKernreactor ; geo:hasGeometry/geo:asWKT ?wkt .
  }
  bind (bif:ST_XMin(?wkt) as ?x1)
  bind (bif:ST_YMin(?wkt) as ?y1)
  bind (bif:ST_XMax(?wkt) as ?x2)
  bind (bif:ST_YMax(?wkt) as ?y2)
  bind (bif:st_distance(?p0, bif:st_point(?x1 + ((?x2 - ?x1) / 2), ?y1 + ((?y2 - ?y1) / 2))) as ?d)
  bind (concat('<p>Afstand tot kerncentrale: ',?d,'km</p>') as ?wktLabel)
}
order by asc(?d)
