PREFIX bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT
  ?Nummeraanduiding_voorkomen ?Nummeraanduiding_voorkomen_beginGeldigheid ?Nummeraanduiding_voorkomen_eindGeldigheid
  ?OpenbareRuimte_voorkomen ?OpenbareRuimte_voorkomen_beginGeldigheid ?OpenbareRuimte_voorkomen_eindGeldigheid
  ?Woonplaats_voorkomen ?Woonplaats_voorkomen_beginGeldigheid ?Woonplaats_voorkomen_eindGeldigheid
WHERE {
  ?Nummeraanduiding a bag:Nummeraanduiding ;
    bag:postcode "1068LE" ;
    bag:huisnummer 300 ;
    bag:bijbehorendeOpenbareRuimte ?OpenbareRuimte ;
    rdfs:isDefinedBy ?Nummeraanduiding_voorkomen .
  ?OpenbareRuimte bag:bijbehorendeWoonplaats ?Woonplaats ;
    rdfs:isDefinedBy ?OpenbareRuimte_voorkomen .
  ?Woonplaats rdfs:isDefinedBy ?Woonplaats_voorkomen .
  ?Nummeraanduiding_voorkomen bag:beginGeldigheid ?Nummeraanduiding_voorkomen_beginGeldigheid .
  ?OpenbareRuimte_voorkomen bag:beginGeldigheid ?OpenbareRuimte_voorkomen_beginGeldigheid .
  ?Woonplaats_voorkomen bag:beginGeldigheid ?Woonplaats_voorkomen_beginGeldigheid .
  OPTIONAL { ?Nummeraanduiding_voorkomen bag:eindGeldigheid ?Nummeraanduiding_voorkomen_eindGeldigheid }
  OPTIONAL { ?OpenbareRuimte_voorkomen bag:eindGeldigheid ?OpenbareRuimte_voorkomen_eindGeldigheid }
  OPTIONAL { ?Woonplaats_voorkomen bag:eindGeldigheid ?Woonplaats_voorkomen_eindGeldigheid }
}
