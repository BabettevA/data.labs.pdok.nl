prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select
  ?nummeraanduidingVoorkomen
  ?nummeraanduidingVoorkomenBegin
  ?nummeraanduidingVoorkomenEind
  ?openbareRuimteVoorkomen
  ?openbareRuimteVoorkomenBegin
  ?openbareRuimteVoorkomenEind
  ?woonplaatsVoorkomen
  ?woonplaatsVoorkomenBegin
  ?woonplaatsVoorkomenEind
{
  ?nummeraanduiding a bag:Nummeraanduiding ;
                    bag:bijbehorendeOpenbareRuimte ?openbareRuimte ;
                    bag:huisnummer 300 ;
                    bag:postcode "1068LE" ;
                    rdfs:isDefinedBy ?nummeraanduidingVoorkomen .
  ?openbareRuimte bag:bijbehorendeWoonplaats ?woonplaats ;
                  rdfs:isDefinedBy ?openbareRuimteVoorkomen .
  ?woonplaats rdfs:isDefinedBy ?woonplaatsVoorkomen .
  ?nummeraanduidingVoorkomen bag:beginGeldigheid ?nummeraanduidingVoorkomenBegin .
  ?openbareRuimteVoorkomen bag:beginGeldigheid ?openbareRuimteVoorkomenBegin .
  ?woonplaatsVoorkomen bag:beginGeldigheid ?woonplaatsVoorkomenBegin .
  optional {
    ?nummeraanduidingVoorkomen bag:eindGeldigheid ?nummeraanduidingVoorkomenEind .
  }
  optional {
    ?openbareRuimteVoorkomen bag:eindGeldigheid ?openbareRuimteVoorkomenEind .
  }
  optional {
    ?woonplaatsVoorkomen bag:eindGeldigheid ?woonplaatsVoorkomenEind .
  }
}
