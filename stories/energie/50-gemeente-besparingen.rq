prefix buurt: <https://krr.triply.cc/Kadaster/cbs/id/buurt/>
prefix cbs: <https://krr.triply.cc/Kadaster/cbs/def/>
prefix gemeente: <https://krr.triply.cc/Kadaster/cbs/id/gemeente/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://krr.triply.cc/Kadaster/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?wkt ?wktLabel {
  bind (strdt("Amsterdam",xsd:string) as ?gemeenteNaam)
  graph graph:cbs-2015 {
    ?gemeente rdfs:label ?gemeenteNaam .
    ?buurt geo:sfWithin/geo:sfWithin ?gemeente ;
           geo:hasGeometry/geo:asWKT ?wkt ;
           rdfs:label ?buurtNaam .
  }
  graph graph:cbs-energie-2015 {
    ?buurt cbs:aardgasverbruikKoopwoning ?gemAardgas ;
           cbs:elektriciteitsverbruikKoopwoning ?gemElektra ;
           cbs:reëleBesparingspotentieAlleMaatregelen ?besparing ;
           cbs:reëleBesparingspotentieBouwfysischeMaatregelen ?bouwfysischeBesparing ;
           cbs:reëleBesparingspotentieInstallatietechnischeMaatregelen ?installatieBesparing ;
           cbs:reëleEenmaligBenodigdeInvesteringAlleMaatregelen ?investering ;
           cbs:reëleEenmaligBenodigdeInvesteringBouwfysischeMaatregeln ?bouwfysischeInvestering ;
           cbs:reëleEenmaligBenodigdeInvesteringInstallatietechnischeMaatregelen ?installatieInvestering .
  }
  bind (?bouwfysischeInvestering + ?installatieInvestering as ?noemer)
  bind (?bouwfysischeBesparing + ?installatieBesparing as ?deler)
  bind (concat(
      '<h3>',str(?buurtNaam),'</h3>',
      '<p>Gemiddeld gebruik aardgas: ',str(?gemAardgas),'m³ per jaar<br>',
      'Gemiddeld gebruik elektriciteit: ',str(?gemElektra),'kWh per jaar</p>',
      '<p>Mogelijke bouwfysische besparingen: €',str(?bouwfysischeBesparing),' per jaar<br>',
      'Mogelijke installatietechnische besparingen: €',str(?installatieBesparing),' per jaar</p>',
      '<p>Bouwfysische investeringen: €',str(?bouwfysischeInvestering),'<br>',
      'Installatietechnische investeringen: €',str(?installatieInvestering),'</p>',
      if(?deler > 0,concat('<p>Positief rendement na ',str(?noemer / ?deler),' jaar.</p>'),'')
    ) as ?wktLabel)
}
limit 20
