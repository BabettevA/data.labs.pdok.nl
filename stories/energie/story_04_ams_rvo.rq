prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
prefix cbs: <https://data.pdok.nl/cbs/def/>
prefix energie: <http://data.labs.pdok.nl/dataset/energie#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://data.labs.pdok.nl/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
# This still returns the same ?pand multiple times, once for each
# energy label that has been assigned to a ?verblijfsobject within
# that ?pand.
select ?pand ?wkt ?wktColor ?wktLabel {
  graph graph:energielabels {
    ?EHouse a energie:HouseEnergyLabel ;
      energie:pand_postcode  ?postcode ;
      energie:pand_huisnummer ?huisnummerString ;
      energie:opname_datum ?opname_datum ;
      energie:registratie_datum ?registratie_datum ;
      energie:e_label ?e_label .
    optional {
      ?EHouse energie:pand_huisnummer_toev ?pans_huisnummer_toev .
    }
    bind (xsd:integer(?huisnummerString) as ?huisnummer)
    bind (if(!(bound(?e_label)), "grey",
             if(strstarts(?e_label, "A"), "#22b14c",
                if(?e_label="B", "#8ff334",
                   if(?e_label="C", "#bdfc2c",
                      if(?e_label="D", "#fff200",
                         if(?e_label="E", "#ff9a35",
                            if(?e_label="F", "#ff7f27",
                               if(?e_label="G", "#ed1c24",
                                  "grey")))))))) as ?wktColor)
  }
  service <https://data.pdok.nl/sparql> {
    graph ?voorkomen1 {
      ?vo bag:identificatiecode ?id ;
        a ?vo_type ;
        bag:oppervlakte ?surface ;
        bag:hoofdadres ?adres ;
        bag:pandrelatering ?pand .
    }
    filter not exists {
      ?voorkomen1 bag:eindGeldigheid ?eind1 .
    }
    graph ?voorkomen2 {
      ?adres bag:huisnummer ?huisnummer ;
        bag:bijbehorendeOpenbareRuimte ?weg ;
      bag:postcode ?postcode .
    }
    filter not exists {
      ?voorkomen2 bag:eindGeldigheid ?eind2 .
    }
    graph ?voorkomen3 {
      ?pand bag:identificatiecode ?pand_id ;
        bag:status ?pandStatus ;
        bag:oorspronkelijkBouwjaar ?bouwjaar ;
        rdfs:isDefinedBy ?pand_defined ;
        geo:hasGeometry/geo:asWKT ?wkt .
    }
    filter not exists {
      ?voorkomen3 bag:eindGeldigheid ?eind3 .
    }
    ?pandStatus rdfs:label ?pandStatusLabel .
    graph ?voorkomen4 {
      ?weg bag:bijbehorendeWoonplaats ?woonplaats ;
        rdfs:label ?straatnaam .
    }
    filter not exists {
      ?voorkomen4 bag:eindGeldigheid ?eind4 .
    }
    ?woonplaats rdfs:label ?woonplaatsLabel .
    bind (concat('<p>address: ',str(?woonplaatsLabel),',  ',str(?postcode),'<br> ',str(?straatnaam),',', str(?huisnummer),'<br>pand status: ',str(?pandStatusLabel),'<br>build year: ',str(?bouwjaar) ,'<br>surface (mÂ²): ',str(?surface),'<br><b>Energy Label:</b>',str(?e_label) ,'<br><b>Energy label date:</b>',str(?registratie_datum),'<br></p>') as ?wktLabel)
  }
}
limit 500
