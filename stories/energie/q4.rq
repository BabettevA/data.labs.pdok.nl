prefix buurt: <https://data.labs.pdok.nl/cbs/id/buurt/>
prefix cbs: <https://data.labs.pdok.nl/cbs/def/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://data.labs.pdok.nl/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?shape ?shapeLabel {
  bind(buurt:BU06370601 as ?buurt)
  graph graph:wijkbuurt-2015 {
    ?buurt geo:hasGeometry/geo:asWKT ?shape ;
           rdfs:label ?buurtNaam .
  }
  graph graph:cbs-energie-2015 {
    ?buurt cbs:aardgasverbruikKoopwoning ?gemAardgas ;
           cbs:elektriciteitsverbruikKoopwoning ?gemElektra ;
           cbs:theoretischeBesparingspotentieAlleMaatregelen ?besparing ;
           cbs:theoretischeBesparingspotentieBouwfysischeMaatregelen ?bouwfysischeBesparing ;
           cbs:theoretischeBesparingspotentieInstallatietechnischeMaatregelen ?installatieBesparing ;
           cbs:theoretischeEenmaligBenodigdeInvesteringAlleMaatregelen ?investering ;
           cbs:theoretischeEenmaligBenodigdeInvesteringBouwfysischeMaatregelen ?bouwfysischeInvestering ;
           cbs:theoretischeEenmaligBenodigdeInvesteringInstallatietechnischeMaatregelen ?installatieInvestering .
  }
  bind((?bouwfysischeInvestering + ?installatieInvestering) / (?bouwfysischeBesparing + ?installatieBesparing) as ?jaren)
  bind(concat('<h3>Zoetermeer ã€‹ ',str(?buurtNaam),'</h3>',
      '<p>ðŸ—² Energiegebruik:</p>',
      '<ul>',
      '<li>Elektriciteit: ',replace(str(?gemElektra),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),'kWh per jaar</li>',
      '<li>Aardgas: ',replace(str(?gemAardgas),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),'mÂ³ per jaar</li>',
      '</ul>',
      '<p>ðŸ’° Besparingen:</p>',
      '<ul>',
      '<li>Bouwfysische: â‚¬ ',replace(str(?bouwfysischeBesparing),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),' per jaar</li>',
      '<li>Installatietechnische: â‚¬ ',replace(str(?installatieBesparing),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),' per jaar</li>',
      '</ul>',
      '<p>ðŸ’° Investeringen:</p>',
      '<ul>',
      '<li>Bouwfysisch: â‚¬ ',replace(str(?bouwfysischeInvestering),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),'</li>',
      '<li>Installatietechnisch: â‚¬ ',replace(str(?installatieInvestering),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),'</li>',
      '</ul>',
      '<p>ðŸ•— Positief rendement na ',replace(str(?jaren),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),' jaar</p>') as ?shapeLabel)
}
limit 1
