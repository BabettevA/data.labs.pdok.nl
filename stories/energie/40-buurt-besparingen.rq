prefix buurt: <https://krr.triply.cc/Kadaster/cbs/id/buurt/>
prefix cbs: <https://krr.triply.cc/Kadaster/cbs/def/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://krr.triply.cc/Kadaster/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select ?wkt ?wktLabel {
  bind (buurt:BU03010500 as ?buurt)
  graph graph:cbs-2015 {
    ?buurt geo:hasGeometry/geo:asWKT ?wkt ;
           rdfs:label ?buurtNaam .
  }
  graph graph:cbs-energie-2015 {
    ?buurt cbs:aardgasverbruikKoopwoning ?gemAardgas ;
           cbs:elektriciteitsverbruikKoopwoning ?gemElektra ;
           cbs:reëleBesparingspotentieAlleMaatregelen ?besparing ;
           cbs:reëleBesparingspotentieBouwfysischeMaatregelen ?bouwfysischeBesparing ;
           cbs:reëleBesparingspotentieInstallatietechnischeMaatregelen ?installatieBesparing ;
           cbs:reëleEenmaligBenodigdeInvesteringAlleMaatregelen ?investering ;
           cbs:reëleEenmaligBenodigdeInvesteringBouwfysischeMaatregeln ?bouwfysischeInvestering ;
           cbs:reëleEenmaligBenodigdeInvesteringInstallatietechnischeMaatregelen ?installatieInvestering .
  }
  bind ((?bouwfysischeInvestering + ?installatieInvestering) / (?bouwfysischeBesparing + ?installatieBesparing) as ?jaren)
  bind (concat('<h3>',str(?buurtNaam),'</h3><p>Gemiddeld gebruik aardgas: ',str(?gemAardgas),'m³ per jaar<br>Gemiddeld gebruik elektriciteit: ',str(?gemElektra),'kWh per jaar</p><p>Mogelijke bouwfysische besparingen: €',str(?bouwfysischeBesparing),' per jaar<br>Mogelijke installatietechnische besparingen: €',str(?installatieBesparing),' per jaar</p><p>Bouwfysische investeringen: €',str(?bouwfysischeInvestering),'<br>Installatietechnische investeringen: €',str(?installatieInvestering),'</p><p>Positief rendement na ',str(?jaren),' jaar.</p>') as ?wktLabel)
}
