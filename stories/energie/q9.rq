prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
prefix cbs: <https://data.pdok.nl/cbs/def/>
prefix energie: <http://data.labs.pdok.nl/dataset/energie#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://data.labs.pdok.nl/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select distinct ?pand ?shape ?shapeColor ?shapeLabel {
  graph graph:energielabels {
    ?EHouse a energie:HouseEnergyLabel ;
      energie:pand_postcode  ?pand_postcode ;
      energie:pand_huisnummer ?huisnummerString ;
      energie:opname_datum ?opname_datum ;
      energie:registratie_datum ?registratie_datum ;
      energie:e_label ?e_label .
    optional {
      ?EHouse energie:pand_huisnummer_toev ?pans_huisnummer_toev .
    }
    bind(xsd:integer(?huisnummerString) as ?huisnummer)
    bind(if(!(bound(?e_label)), "grey",
         if(strstarts(?e_label,"A"), "#22b14c",
         if(?e_label="B", "#8ff334",
         if(?e_label="C", "#bdfc2c",
         if(?e_label="D", "#fff200",
         if(?e_label="E", "#ff9a35",
         if(?e_label="F", "#ff7f27",
         if(?e_label="G", "#ed1c24", "grey")))))))) as ?shapeColor)
  }
  service <https://data.pdok.nl/sparql> {
    ?vo bag:identificatiecode ?id ;
        a ?vo_type ;
        bag:oppervlakte ?oppervlakte ;
        bag:hoofdadres ?adres ;
        bag:status [rdfs:label ?vo_status ];
        bag:pandrelatering ?pand .
    ?adres bag:huisnummer ?huisnummer ;
           bag:bijbehorendeOpenbareRuimte ?weg ;
           bag:postcode  ?pand_postcode .
    ?pand bag:identificatiecode ?pand_id ;
        bag:status [rdfs:label ?pand_status] ;
        bag:oorspronkelijkBouwjaar ?build_year ;
        rdfs:isDefinedBy ?pand_defined ;
        geo:hasGeometry/geo:asWKT ?shape .
    ?weg rdfs:label ?straatname ;
          bag:bijbehorendeWoonplaats/rdfs:label ?woonplaats .
    bind(concat('<p>Adres: ',str(?woonplaats),',  ',str(?pand_postcode),'<br>',
                str(?straatname),',  ', str(?huisnummer) ,'<br>',
                'Pand status: ',str(?pand_status),'<br>',
                'Bouwjaar: ',str(?build_year) ,'<br>',
                'Oppervlakte (mÂ²): ',str(?oppervlakte),'<br>',
                'Energie label: ',str(?e_label),'<br>',
                'Energie label datum: ',str(?registratie_datum),'</p>') as ?shapeLabel)
  }
  filter((?build_year<1950) && (?e_label='A'))
}
limit 100
