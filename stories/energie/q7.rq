prefix buurt: <https://data.labs.pdok.nl/cbs/id/buurt/>
prefix cbs: <https://data.labs.pdok.nl/cbs/def/>
prefix gemeente: <https://data.labs.pdok.nl/cbs/id/gemeente/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://data.labs.pdok.nl/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?shape ?shapeColor ?shapeLabel {
  bind(strdt("Amsterdam",xsd:string) as ?gemeenteNaam)
  graph graph:wijkbuurt-2015 {
    ?buurt geo:hasGeometry/geo:asWKT ?shape ;
           geo:sfWithin/geo:sfWithin/rdfs:label ?gemeenteNaam ;
           rdfs:label ?buurtNaam .
   }
  graph graph:cbs-energie-2015 {
    ?buurt cbs:aardgasverbruikKoopwoning ?gemAardgas ;
           cbs:elektriciteitsverbruikKoopwoning ?gemElektra ;
           cbs:reÃ«leBesparingspotentieAlleMaatregelen ?besparing ;
           cbs:reÃ«leBesparingspotentieBouwfysischeMaatregelen ?bouwfysischeBesparing ;
           cbs:reÃ«leBesparingspotentieInstallatietechnischeMaatregelen ?installatieBesparing ;
           cbs:reÃ«leEenmaligBenodigdeInvesteringAlleMaatregelen ?investering ;
           cbs:reÃ«leEenmaligBenodigdeInvesteringBouwfysischeMaatregeln ?bouwfysischeInvestering ;
           cbs:reÃ«leEenmaligBenodigdeInvesteringInstallatietechnischeMaatregelen ?installatieInvestering .
  }
  bind(?bouwfysischeInvestering + ?installatieInvestering as ?noemer)
  bind(?bouwfysischeBesparing + ?installatieBesparing as ?deler)
  bind(xsd:float(?noemer)/?deler as ?jaren)
  bind(if(?deler>0, (22.0-(xsd:float(?noemer)/?deler-8.0))/22.0, 0) as ?normalized)
  bind(concat("bluered,",str(?normalized)) as ?shapeColor)
  bind(concat('<h3>Amsterdam ã€‹ ',str(?buurtNaam),'</h3>',
      '<p>ðŸ—² Energiegebruik:</p>',
      '<ul>',
      '<li>Elektriciteit: ',replace(str(?gemElektra),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),'kWh per jaar</li>',
      '<li>Aardgas: ',replace(str(?gemAardgas),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),'mÂ³ per jaar</li>',
      '</ul>',
      '<p>ðŸ’° Besparingen:</p>',
      '<ul>',
      '<li>Bouwfysische: â‚¬ ',replace(str(?bouwfysischeBesparing),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),' per jaar</li>',
      '<li>Installatietechnische: â‚¬ ',replace(str(?installatieBesparing),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),' per jaar</li>',
      '</ul>',
      '<p>ðŸ’° Investeringen:</p>',
      '<ul>',
      '<li>Bouwfysisch: â‚¬ ',replace(str(?bouwfysischeInvestering),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),'</li>',
      '<li>Installatietechnisch: â‚¬ ',replace(str(?installatieInvestering),'[0-9](?=(?:[0-9]{3})+(?![0-9]))','$0.'),'</li>',
      '</ul>',
      if(bound(?normalized),
         concat('<p>ðŸ•— Positief rendement na ',substr(str(?jaren), 1, 4),' jaar.</p>'),
         '')) as ?shapeLabel)
}
limit 75
