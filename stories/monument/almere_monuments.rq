prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix dct: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://data.labs.pdok.nl/graph/>
prefix rce: <https://data.labs.pdok.nl/rce/def/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select * {
  bind("Amersfoort" as ?plaatsNaam)
  bind(strlang(?plaatsNaam, "nl-nl") as ?plaatsNaamNL)
  {
    select ?huisnummer ?huisnummerString ?plaatsNaamNL ?monument ?monumentNaam ?straatNaam ?straatNaamNL (group_concat(?afbeeldingLabel) as ?afbeeldingLabel) {
      graph graph:beeldbank {
        ?monument rce:huisnummer ?huisnummerString ;
                  rce:plaatsnaam ?plaatNaamNL ;
                  rce:straat ?straatNaamNL ;
                  rdfs:label ?monumentNaam .
        ?afbeelding dct:created ?datum ;
                    foaf:depicts ?monument ;
                    rce:fotograaf ?fotograaf ;
                    rce:locator ?url .
      }
      bind(concat(
        '<a href="',str(?url),'" target="_blank"><img src="',str(?url),'" style="width: 225px;"></a>',
        '<p>',str(?fotograaf),', ',str(?datum),'</p>') as ?afbeeldingLabel)
      bind(xsd:integer(?huisnummerString) as ?huisnummer)
      bind(str(?straatNaamNL) as ?straatNaam)
    }
    group by ?huisnummerString ?plaatsNaamNL ?monument ?monumentNaam ?straatNaam ?straatNaamNL
  }
  bind(concat(
    '<h3><a href="',str(?monument),'">',str(?monumentNaam),'</a></h3>',
    '<h3>',str(?straatNaamNL),' ',str(?huisnummerString),', ',str(?plaatsNaamNL),'</a></h3>',
    ?afbeeldingLabel) as ?monumentLabel)
  service <https://data.pdok.nl/sparql> {
    ?plaats bag:naamWoonplaats ?plaatsNaam .
    ?openbareRuimte bag:bijbehorendeWoonplaats ?plaats ;
                    bag:naamOpenbareRuimte ?straatNaam .
    ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte ;
                      bag:huisnummer ?huisnummer .
    ?verblijfsobject bag:hoofdadres ?nummeraanduiding ;
                     bag:oppervlakte ?oppervlakte ;
                     bag:pandrelatering ?pand ;
                     bag:status ?verblijfsobjectStatus .
    ?verblijfsobjectStatus rdfs:label ?verblijfsobjectStatusLabel .
    ?pand bag:geometriePand/geo:asWKT ?pandWkt ;
          bag:oorspronkelijkBouwjaar ?buildYear ;
          bag:status ?pandStatus .
    ?pandStatus rdfs:label ?pandStatusLabel
  }
  bind(concat(
    str(?monumentLabel),
    '<p><a href="',str(?verblijfsobject),'">Verblijfsobject</a> met ',str(?oppervlakte),'mÂ² oppervlakte en status <a href="',str(?verblijfsobjectStatus),'">',str(?verblijfsobjectStatusLabel),'</a></p>',
    '<p><a href="',str(?pand),'">Pand</a> met bouwjaar ',str(?buildYear),' en status <a href="',str(?pandStatus),'">',str(?pandStatusLabel),'</a></p>') as ?pandWktLabel)
}
limit 25
