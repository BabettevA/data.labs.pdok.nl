PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gwsw: <http://data.gwsw.nl/1.4/totaal/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
PREFIX pdok: <http://data.pdok.nl/def/pdok#>

SELECT distinct ?wkt ?wktLabel ?wktColor WHERE {
  {
    ?gem_uri a brt:Gemeente ;
             rdfs:label ?gem_naam ;
             brt:geometrie ?gem_geom .
    ?gem_geom geo:asWKT ?wkt_gem .
    bind(geof:buffer(?wkt_gem, 0.0005) as ?wkt)
    bind(concat("BRT Gemeente <a href='", str(?gem_uri), "' target='_blank'><h3>", str(?gem_naam), "</h3></a>") as ?wktLabel)
    bind("orange" as ?wktColor)
    filter(?gem_naam = "Castricum"@nl)
  } UNION  {
    SERVICE <https://sparql.gwsw.nl/repositories/TestDatastory> {
      ?rg a gwsw:Rioleringsgebied ;
          rdfs:label ?wktlabel ;
          gwsw:hasAspect ?ori .
      ?ori a gwsw:Gebiedsorientatie ;
           gwsw:hasAspect ?bg .
      ?bg a gwsw:Buitengrens ;
          geo:asWKT ?wkt .
      filter(contains(?wktlabel, "Castricum"))
      bind("green" as ?wktColor)
    }
  } UNION {
    SERVICE <https://sparql.gwsw.nl/repositories/TestDatastory> {
      ?rg a gwsw:Rioleringsgebied ;
          rdfs:label ?wktlabel ;
          gwsw:hasAspect ?ori .
      ?ori a gwsw:Gebiedsorientatie ;
           gwsw:hasAspect ?bg .
      ?bg a gwsw:Buitengrens ;
          geo:asWKT ?wkt_gwsw .
      filter(contains(?wktlabel, "Castricum"))
    }

    ?gem_uri a brt:Gemeente ;
      rdfs:label ?gem_naam ;
      brt:geometrie ?gem_geom .
    ?gem_geom geo:asWKT ?wkt_gem .
    bind(geof:buffer(?wkt_gem, 0.0005) as ?wkt_gem_buffer)
    filter(?gem_naam = "Castricum"@nl)

    ?wp a bag:Woonplaats ;
        rdfs:label ?wp_naam ;
        geo:hasGeometry ?wp_geom .
    ?wp_geom geo:asWKT ?wkt .
    filter(geof:sfWithin(?wkt, ?wkt_gem_buffer))
    filter(geof:sfIntersects(?wkt, ?wkt_gwsw))

    bind(concat("BAG Woonplaats <a href='", str(?wp), "' target='_blank'><h3>", str(?wp_naam), "</h3></a>") as ?wktLabel)
  }
}
