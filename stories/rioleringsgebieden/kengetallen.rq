# SPARQL-endpoint = https://betalinkeddata.cbs.nl/sparql 

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gwsw: <http://data.gwsw.nl/1.4/totaal/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
PREFIX pdok: <http://data.pdok.nl/def/pdok#>
PREFIX cbs: <http://betalinkeddata.cbs.nl/def/cbs#>
PREFIX cbs_dim: <http://betalinkeddata.cbs.nl/def/dimension#>
PREFIX cbs_slice: <http://betalinkeddata.cbs.nl/83487NED/id/slice/>
PREFIX cbs_qb: <http://betalinkeddata.cbs.nl/def/cbs-qb#>
PREFIX cbs_83487: <http://betalinkeddata.cbs.nl/def/83487NED#>
PREFIX sesame: <http://www.openrdf.org/schema/sesame#>

#SELECT ?gml_EPSG4258 ?rg_wkt ?buurt ?wkt ?wktLabel
#SELECT ?buurt ?wkt ?rg_wkt
#SELECT ?buurt ?wkt_rd ?geo
SELECT ?rg_naam ?lei_lengte ?gemeente_naam ?wijk_naam ?buurt_naam ?inwoners_aantal ?huishoudens_aantal ?bedrijven_aantal ?recreatie_aantal

WHERE
{
  SERVICE <https://sparql.gwsw.nl/repositories/TestDatastory>
  {
    SELECT ?rg_naam ?rg_wkt (SUM (?lengte) AS ?lei_lengte)

    where
    {
      ?rg a gwsw:Rioleringsgebied ;
          rdfs:label ?rg_naam ;
          gwsw:hasAspect ?ori .
      ?ori a gwsw:Gebiedsorientatie ;
           gwsw:hasAspect ?bg .
      ?bg a gwsw:Buitengrens ;
          #gwsw:hasValue ?geo ; # GWSW-default = EPSG:28992 in GML vorm
		  geo:asWKT ?rg_wkt . # Alternatief = WGS84 in WKT vorm

      ?rg gwsw:hasPart ?rs .
      ?rs rdfs:label ?rs_naam ;
          gwsw:hasPart ?lei .
      ?lei a gwsw:Leiding .
      #?lei sesame:directType ?lei_type .
      ?lei gwsw:hasAspect [
          a gwsw:LengteLeiding ;
          gwsw:hasValue ?lengte ;
		  ] .
      #filter(contains(?rg_naam, "pancras"))
    }
    GROUP BY ?rg_naam ?rg_wkt
  }
  { ?gemeente rdfs:label "Castricum"@nl . }
  UNION
  { ?gemeente rdfs:label "Alkmaar"@nl . }

  ?gemeente rdfs:label ?gemeente_naam .

  ?buurt a cbs:Buurt ;
         rdfs:label ?buurt_naam ;
         geo:hasGeometry
     	 [  geo:asWKT ?buurt_wkt ;
      		#pdok:asWKT-RD ?buurt_wkt_rd ;
  		 ] ;
        ?within ?wijk .
  ?wijk a cbs:Wijk ;
         rdfs:label ?wijk_naam ;
         geo:hasGeometry
     	 [  geo:asWKT ?wijk_wkt ;
      		#pdok:asWKT-RD ?wijk_wkt_rd ;
         ] ;
        ?within ?gemeente .

  #?geom geo:asWKT ?wkt .

  #filter(geof:sfIntersects(?buurt_wkt_rd, ?geo))
  filter (geof:sfIntersects(?buurt_wkt, ?rg_wkt))
  #filter (geof:sfContains(?wijk_wkt, ?buurt_wkt))

  # Veelzeggende eigenschappen van de buurt
  ?inwoners cbs_dim:regio ?buurt ;
            cbs_qb:inObservationGroup cbs_slice:bevolking_AantalInwoners ;
            cbs_83487:bevolking_AantalInwoners ?inwoners_aantal .

  ?huishoudens cbs_dim:regio ?buurt ;
               cbs_qb:inObservationGroup cbs_slice:bevolking_ParticuliereHuishoudens_HuishoudensTotaal ;
               cbs_83487:bevolking_ParticuliereHuishoudens_HuishoudensTotaal ?huishoudens_aantal .

  optional
  {
    ?bedrijven cbs_dim:regio ?buurt ;
               cbs_qb:inObservationGroup cbs_slice:bedrijfsvestigingen_Sbi2008_BedrijfsvestigingenTotaal ;
               cbs_83487:bedrijfsvestigingen_Sbi2008_BedrijfsvestigingenTotaal ?bedrijven_aantal .
  }
  optional
  {
    ?recreatie cbs_dim:regio ?buurt ;
               cbs_qb:inObservationGroup cbs_slice:bedrijfsvestigingen_Sbi2008_BedrijfsvestigingenNaarActiviteit_R-uCultuur_Recreatie_OverigeDiensten ;
               cbs_83487:bedrijfsvestigingen_Sbi2008_BedrijfsvestigingenNaarActiviteit_R-uCultuur_Recreatie_OverigeDiensten ?recreatie_aantal .
  }
  #bind(concat(
  #    "<a href='", str(?buurt), "' target='_blank'><h3>", str(?buurt_label), "</h3></a>",
  #    "<p>Aantal inwoners: <a target='_blank' href='", str(?aInwoners_obs), "'>", str(?aInwoners), "</a></p>",
  #    "<p>Aantal huishoudens:  <a target='_blank' href='", str(?aHuishoudens_obs), "'>", str(?aHuishoudens), "</p>"
  #  ) as ?wktLabel)
}
limit 200
