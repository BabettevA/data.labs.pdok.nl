PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX gwsw: <http://data.gwsw.nl/1.4/totaal/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX brt: <http://brt.basisregistraties.overheid.nl/def/top10nl#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
PREFIX pdok: <http://data.pdok.nl/def/pdok#>
PREFIX cbs: <http://betalinkeddata.cbs.nl/def/cbs#>
PREFIX cbs_dim: <http://betalinkeddata.cbs.nl/def/dimension#>
PREFIX cbs_slice: <http://betalinkeddata.cbs.nl/83487NED/id/slice/>
PREFIX cbs_qb: <http://betalinkeddata.cbs.nl/def/cbs-qb#>
PREFIX cbs_83487: <http://betalinkeddata.cbs.nl/def/83487NED#>

SELECT ?buurt ?wkt ?wktLabel WHERE {
  SERVICE <https://sparql.gwsw.nl/repositories/TestVali> {
      ?rg a gwsw:Rioleringsgebied ;
          rdfs:label ?rg_naam ;
          gwsw:hasAspect ?ori .
      ?ori a gwsw:Gebiedsorientatie ;
           gwsw:hasAspect ?bg .
      ?bg a gwsw:Buitengrens ;
          gwsw:hasValue ?gml .
    
#mv: (20180828) dit filter is niet nodig, de GWSW-Repository bevat de selectie aan rioleringsgebieden
      filter(contains(?rg_naam, "pancras"))
  }

#mv: (20180828) ook zonder onderstaand filter blijft de response-tijd acceptabel
  {
    ?cbs_gemeente rdfs:label "Langedijk"@nl .
  } UNION {
    ?cbs_gemeente rdfs:label "Alkmaar"@nl .
  }

  ?buurt a cbs:Buurt;
         rdfs:label ?buurt_label ;
         geo:hasGeometry ?geom ;
         ?within ?wijk .
  ?wijk a cbs:Wijk;
        ?within ?cbs_gemeente .

  ?geom geo:asWKT ?wkt ;
        pdok:asWKT-RD ?wkt_rd .

  filter(geof:sfIntersects(?wkt_rd, ?gml))

  # Veelzeggende eigenschappen van de buurt
  ?aantal_inwoners_obs cbs_dim:regio ?buurt ;
                       cbs_qb:inObservationGroup cbs_slice:bevolking_AantalInwoners ;
                       cbs_83487:bevolking_AantalInwoners ?aantal_inwoners .

  ?aantal_huishoudens_obs cbs_dim:regio ?buurt ;
                          cbs_qb:inObservationGroup cbs_slice:bevolking_ParticuliereHuishoudens_HuishoudensTotaal ;
                          cbs_83487:bevolking_ParticuliereHuishoudens_HuishoudensTotaal ?aantal_huishoudens .

  bind(concat(
      "<a href='", str(?buurt), "' target='_blank'><h2>", str(?buurt_label), "</h2></a>",
      "<p>Aantal inwoners: <a target='_blank' href='", str(?aantal_inwoners_obs), "'>", str(?aantal_inwoners), "</a></p>",
      "<p>Aantal huishoudens:  <a target='_blank' href='", str(?aantal_huishoudens_obs), "'>", str(?aantal_huishoudens), "</p>"
    ) as ?wktLabel)

}
