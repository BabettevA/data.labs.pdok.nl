prefix def: <http://data.pdok.nl/mossel-en-oesterhabitats/def/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix n2k: <http://data.pdok.nl/natura-2000/def/natura-2000#>
select ?shape ?shapeLabel ?shapeColor {
  {
    {
      ?sub n2k:sitecode_h 'NL2007001'
    } union {
      ?sub n2k:sitecode_h 'NL1000001'
    } union {
      ?sub n2k:sitecode_v 'NL9801001'
    }
    ?sub a n2k:Natura2000 ;
         geo:hasGeometry/geo:asWKT ?shape ;
         n2k:bescherming ?bescherming ;
         rdfs:label ?label .
    bind(concat(?label,' | ',?bescherming) as ?shapeLabel)
  } union {
    ?x a def:mossel-en-oesterhabitats\#Mosselenoesterhabitats ;
       geo:hasGeometry/geo:asWKT ?shape;
       rdfs:label ?shapeLabel .
    values (?shapeLabel ?shapeColor) {
      ('mussel' 'red')
      ('oyster' 'blue')
      ('mussel/oyster' 'green')
    }
  }
}
